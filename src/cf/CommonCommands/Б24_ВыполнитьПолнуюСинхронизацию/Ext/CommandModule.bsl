
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	КоличествоНастроек = Б24_СинхронизацияВызовСервера.ПолучитьКоличествоНастроекСинхронизации();

	Если КоличествоНастроек > 0 тогда
	
		Режим = РежимДиалогаВопрос.ДаНет;
		
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса",ЭтотОбъект);
		ПоказатьВопрос(Оповещение, "Данную операцию рекомендуется выполнять, когда или источник или приемник пустой. Вы точно желаете выполнить полную синхронизацию даных?", Режим, 0);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	
	КоличествоНастроек = Б24_СинхронизацияВызовСервера.ПолучитьКоличествоНастроекСинхронизации();
	
	Если КоличествоНастроек > 1 тогда
		
		ВыбЗнач = Неопределено;
		
		Массив = Новый Массив;
		Массив.Добавить(Тип("СправочникСсылка.Б24_НастройкиСинхронизацииСБитрикс24"));
		ОписаниеТипов = Новый ОписаниеТипов(Массив);
		
		Оповещение = Новый ОписаниеОповещения("ПослеВводаЗначения",ЭтотОбъект );

		ПоказатьВводЗначения(Оповещение, ВыбЗнач, "Укажите настройку синхронизации", ОписаниеТипов);	
	Иначе
		Параметры = Новый Структура;	
		ПослеВводаЗначения(Б24_СинхронизацияВызовСервера.ПолучитьНастройкуСинхронизацииПоУмолчанию(), Параметры);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаЗначения(ВыбЗнач, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(ВыбЗнач) Тогда

		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("НастройкаСинхронизации", ВыбЗнач);
		ПараметрыФормы.Вставить("ВидСинхронизации"		, 2);
		ПараметрыФормы.Вставить("ПолнаяСинхронизация"	, Истина);
		ПараметрыФормы.Вставить("ВыполнятьЗагрузку"		, Неопределено);
		ПараметрыФормы.Вставить("ВыполнятьВыгрузку"		, Неопределено);
		
		ОткрытьФорму("Справочник.Б24_НастройкиСинхронизацииСБитрикс24.Форма.ФормаВыполнениеСинхронизации", ПараметрыФормы, , ВыбЗнач);
	
	КонецЕсли;
	
КонецПроцедуры