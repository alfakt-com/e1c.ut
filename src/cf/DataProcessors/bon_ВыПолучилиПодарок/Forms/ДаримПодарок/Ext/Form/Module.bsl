
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Склад = Параметры.Склад;
	Объект.МаркетинговаяАкция = Параметры.МаркетинговаяАкция;
	
	ЗаполнитьНоменклатуру();
	
	Элементы.ПодарокНеНужен.Доступность = Альфа_КТ.ПроверитьДоступ("ЗаказКлиентаОтказотПодарка", ПараметрыСеанса.ТекущийПользователь, ТекущаяДата(), ТекущаяДата());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатуру()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	bon_ВыдаваемыеЦенностиСрезПоследних.Значение КАК ВыдаваемыйПодарок
	|ПОМЕСТИТЬ Подарки
	|ИЗ
	|	РегистрСведений.bon_ВыдаваемыеЦенности.СрезПоследних(
	|			,
	|			ТипЦенности = ЗНАЧЕНИЕ(Перечисление.bon_ТипЦенности.Подарок)
	|				И МаркетинговойАкции = &МаркетинговойАкции) КАК bon_ВыдаваемыеЦенностиСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвободныеОстаткиОстатки.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрНакопления.СвободныеОстатки.Остатки(
	|			,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						Подарки.ВыдаваемыйПодарок КАК ВыдаваемыйПодарок
	|					ИЗ
	|						Подарки КАК Подарки)
	|				И Склад = &Склад) КАК СвободныеОстаткиОстатки
	|ГДЕ
	|	ЕСТЬNULL(СвободныеОстаткиОстатки.ВНаличииОстаток, 0) - ЕСТЬNULL(СвободныеОстаткиОстатки.ВРезервеСоСкладаОстаток, 0) - ЕСТЬNULL(СвободныеОстаткиОстатки.ВРезервеПодЗаказОстаток, 0) > 0";
	Запрос.УстановитьПараметр("МаркетинговойАкции", Объект.МаркетинговаяАкция);
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	СписокПодарков.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	ЗаполнитьНоменклатуру();
КонецПроцедуры

&НаКлиенте
Процедура МаркетинговаяАкцияПриИзменении(Элемент)
	ЗаполнитьНоменклатуру();
КонецПроцедуры

&НаКлиенте
Процедура СписокПодарковВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	//Апаев_О Начало [16.07.2022] - "Доработка подарков акции"	
	//Подарок = СписокПодарков.НайтиПоИдентификатору(ВыбраннаяСтрока).Номенклатура;
	//ОповеститьОВыборе(Подарок);
	
	МассивПодарков = Новый Массив;
	
	Для Каждого Строка Из ВыбраннаяСтрока Цикл
		МассивПодарков.Добавить(СписокПодарков.НайтиПоИдентификатору(Строка).Номенклатура);
	КонецЦикла;
	
	ОповеститьОВыборе(МассивПодарков);
	//Апаев_О Окончание [16.07.2022]
	
КонецПроцедуры

&НаКлиенте
Процедура ПодарокНеНужен(Команда)
	ОповеститьОВыборе(Истина);
КонецПроцедуры

