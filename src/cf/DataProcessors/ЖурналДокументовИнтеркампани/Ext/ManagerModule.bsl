
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт

КонецПроцедуры

#КонецОбласти

// Определяет состав документов и хозяйственных операций, доступных для отображения в рабочем месте.
//
// Параметры:
//  ОтборХозяйственныеОперации		 - СписокЗначений - список значений типа ПеречислениеСсылка.ХозяйственныеОперации
//  ОтборТипыДокументов				 - СписокЗначений - список значений типа СправочникСсылка.ИдентификаторыОбъектовМетаданных
//  КлючНазначенияИспользования		 - Строка - ключ рабочего места для которого вызывается функция.
// 
// Возвращаемое значение:
//   - ТаблицаЗначений - см. ОбщегоНазначенияУТ.НоваяТаблицаХозяйственныеОперацииИДокументы 
//
Функция ИнициализироватьХозяйственныеОперацииИДокументы(ОтборХозяйственныеОперации, ОтборТипыДокументов, КлючНазначенияИспользования) Экспорт
	
	ХозяйственныеОперацииИДокументы = ХозяйственныеОперацииИДокументы();
	
	Если КлючНазначенияИспользования = "ДокументыИнтеркампани" Тогда
		КлючНастроек = "";
	Иначе
		КлючНастроек = КлючНазначенияИспользования;
	КонецЕсли;
	
	ТаблицаЗначенийДоступно = ОбщегоНазначенияУТ.ДоступныеХозяйственныеОперацииИДокументы(ХозяйственныеОперацииИДокументы,
		ОтборХозяйственныеОперации, ОтборТипыДокументов, КлючНастроек);
	
	Возврат ТаблицаЗначенийДоступно;
	
КонецФункции

#Область ФормированиеГиперссылкиВЖурналеДокументовЗакупки

// Возвращает текст запроса, который используется для получения данных об оформляемых отчетах
// по комиссиям между организациями.
//
// Параметры;
//	ПараметрыЗапроса - Структура - параметры запроса.
//
// ВозвращаемоеЗначение:
//	Строка - текст запроса, который используется для получения данных об оформляемых отчетах
//		по комиссиям между организациями.
//
Функция ТекстЗапросаОформляемыеОтчетыПоКомиссии(ПараметрыЗапроса) Экспорт
	
	ТекстЗапросаВременныхТаблиц   = "";
	ТекстЗапросаТоваровКОформлению = "";
	
	ТекстОбъединить = "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|";
	ТекстРазделить  = "
		|;
		|
		|//////////////////////////////////////////////////
		|";
	
	ЕстьПравоЧтенияТоваровКОформлению = ПравоДоступа(
		"Чтение", Метаданные.РегистрыНакопления.ТоварыКОформлениюОтчетовКомитенту);
	ЕстьПравоЧтенияТоваровПереданных  = ПравоДоступа(
		"Чтение", Метаданные.РегистрыНакопления.ТоварыПереданныеНаКомиссию);
	ЕстьПравоЧтенияУслугКОформлению   = ПравоДоступа(
		"Чтение", Метаданные.РегистрыНакопления.УслугиКОформлениюОтчетовПринципалу);
	
	Если ПараметрыЗапроса.ОформитьПоОтчетамКомиссии
		И ЕстьПравоЧтенияТоваровКОформлению
		И ЕстьПравоЧтенияТоваровПереданных Тогда
		
		ТекстЗапросаВременныхТаблиц = ТекстЗапросаВременныхТаблиц + "
		|ВЫБРАТЬ
		|	ТоварыКомитенту.Валюта                                    КАК Валюта,
		|	ТоварыКомитенту.ВидЗапасов                                КАК ВидЗапасов,
		|	ТоварыКомитенту.ВидЗапасов.Организация                    КАК Комиссионер,
		|	ТоварыКомитенту.ВидЗапасов.ВладелецТовара                 КАК Комитент,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры                КАК АналитикаУчетаНоменклатуры,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Назначение     КАК Назначение,
		|	СУММА(ТоварыКомитенту.КоличествоОстаток)                  КАК Остаток
		|ПОМЕСТИТЬ ТоварыКомитентуНаКонецПериода
		|ИЗ
		|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту.Остатки(&КонГраница,
		|		ВидЗапасов.ВладелецТовара ССЫЛКА Справочник.Организации
		|		//&УсловиеОтбора1
		|	) КАК ТоварыКомитенту
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыКомитенту.Валюта,
		|	ТоварыКомитенту.ВидЗапасов,
		|	ТоварыКомитенту.ВидЗапасов.Организация,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Номенклатура,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Характеристика,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Назначение
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТоварыКомитенту.КоличествоОстаток) <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Комиссионер,
		|	Комитент,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Склад          КАК Комиссионер,
		|	ТоварыПереданные.ВидЗапасов.Организация                    КАК Комитент,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры                КАК АналитикаУчетаНоменклатуры,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Назначение     КАК Назначение,
		|	СУММА(ТоварыПереданные.КоличествоОстаток)                  КАК Остаток
		|ПОМЕСТИТЬ ТоварыПереданныеНаКонецПериода
		|ИЗ
		|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(&КонГраница,
		|		АналитикаУчетаНоменклатуры.Склад ССЫЛКА Справочник.Организации
		|		//&УсловиеОтбора2
		|	) КАК ТоварыПереданные
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Склад,
		|	ТоварыПереданные.ВидЗапасов.Организация,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Номенклатура,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Характеристика,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Назначение
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТоварыПереданные.КоличествоОстаток) <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Комиссионер,
		|	Комитент,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////";
		
		ТекстЗапросаТоваровКОформлению = ТекстЗапросаТоваровКОформлению + "
		|ВЫБРАТЬ //&КоличествоЗаписей
		|	ТоварыКомитенту.Валюта                     КАК Валюта,
		|	ТоварыКомитенту.Комиссионер                КАК Комиссионер,
		|	ТоварыКомитенту.Комитент                   КАК Комитент,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТоварыКомитенту.ВидЗапасов                 КАК ВидЗапасов,
		|	ВЫБОР
		|		КОГДА ТоварыКомитенту.Остаток <= ТоварыПереданные.Остаток
		|			ТОГДА ТоварыКомитенту.Остаток
		|		КОГДА ТоварыКомитенту.Остаток > ТоварыПереданные.Остаток
		|			ТОГДА ТоварыПереданные.Остаток
		|	КОНЕЦ                                      КАК Остаток,
		|	0                                          КАК Списано,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациями) КАК ХозяйственнаяОперация
		|ИЗ
		|	ТоварыКомитентуНаКонецПериода КАК ТоварыКомитенту
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыПереданныеНаКонецПериода КАК ТоварыПереданные
		|		ПО ТоварыКомитенту.Комиссионер = ТоварыПереданные.Комиссионер
		|			И ТоварыКомитенту.Комитент = ТоварыПереданные.Комитент
		|			И ТоварыКомитенту.Номенклатура = ТоварыПереданные.Номенклатура
		|			И ТоварыКомитенту.Характеристика = ТоварыПереданные.Характеристика
		|			И ТоварыКомитенту.Назначение = ТоварыПереданные.Назначение
		|
		|ГДЕ
		|	ВЫБОР
		|		КОГДА ТоварыКомитенту.Остаток <= ТоварыПереданные.Остаток
		|			ТОГДА ТоварыКомитенту.Остаток
		|		КОГДА ТоварыКомитенту.Остаток > ТоварыПереданные.Остаток
		|			ТОГДА ТоварыПереданные.Остаток
		|	КОНЕЦ <> 0
		|";
		
	КонецЕсли;
	
	Если ПараметрыЗапроса.ОформитьПоОтчетамКомиссии
		И ЕстьПравоЧтенияУслугКОформлению Тогда
		
		ТекстЗапросаТоваровКОформлению = ?(ЗначениеЗаполнено(ТекстЗапросаТоваровКОформлению),
										ТекстЗапросаТоваровКОформлению + ТекстОбъединить,
										ТекстЗапросаТоваровКОформлению);
		
		ТекстЗапросаТоваровКОформлению = ТекстЗапросаТоваровКОформлению + "
		|ВЫБРАТЬ //&КоличествоЗаписей
		|	УслугиКОформлению.Валюта                           КАК Валюта,
		|	УслугиКОформлению.Организация                      КАК Комиссионер,
		|	УслугиКОформлению.АналитикаУчетаНоменклатуры.Склад КАК Комитент,
		|	УслугиКОформлению.АналитикаУчетаНоменклатуры       КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)      КАК ВидЗапасов,
		|	УслугиКОформлению.КоличествоОстаток                КАК Остаток,
		|	0                                                  КАК Списано,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациями) КАК ХозяйственнаяОперация
		|ИЗ
		|	РегистрНакопления.УслугиКОформлениюОтчетовПринципалу.Остатки(&КонГраница,
		|		АналитикаУчетаНоменклатуры.Склад ССЫЛКА Справочник.Организации
		|		//&УсловиеОтбора3
		|	) КАК УслугиКОформлению
		|ГДЕ
		|	УслугиКОформлению.КоличествоОстаток <> 0";
		
	КонецЕсли;
	
	Если ПараметрыЗапроса.ОтобратьПоОтчетамКомиссииОСписании
		И ЕстьПравоЧтенияТоваровКОформлению
		И ЕстьПравоЧтенияТоваровПереданных Тогда
		
		ТекстЗапросаВременныхТаблиц = ТекстЗапросаВременныхТаблиц + "
		|ВЫБРАТЬ
		|	ТоварыКомитенту.Валюта                                    КАК Валюта,
		|	ТоварыКомитенту.ВидЗапасов                                КАК ВидЗапасов,
		|	ТоварыКомитенту.ВидЗапасов.Организация                    КАК Комиссионер,
		|	ТоварыКомитенту.ВидЗапасов.ВладелецТовара                 КАК Комитент,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры                КАК АналитикаУчетаНоменклатуры,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Назначение     КАК Назначение,
		|	СУММА(ТоварыКомитенту.КоличествоСписаноОстаток)           КАК Списано
		|ПОМЕСТИТЬ ТоварыКомитентуСписываемыеНаКонецПериода
		|ИЗ
		|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту.Остатки(&КонГраница,
		|		ВидЗапасов.ВладелецТовара ССЫЛКА Справочник.Организации
		|		//&УсловиеОтбора1
		|	) КАК ТоварыКомитенту
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыКомитенту.Валюта,
		|	ТоварыКомитенту.ВидЗапасов,
		|	ТоварыКомитенту.ВидЗапасов.Организация,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Номенклатура,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Характеристика,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры.Назначение
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТоварыКомитенту.КоличествоСписаноОстаток) <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Комиссионер,
		|	Комитент,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Склад          КАК Комиссионер,
		|	ТоварыПереданные.ВидЗапасов.Организация                    КАК Комитент,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры                КАК АналитикаУчетаНоменклатуры,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Назначение     КАК Назначение,
		|	СУММА(ТоварыПереданные.КоличествоОстаток)                  КАК Списано
		|ПОМЕСТИТЬ ТоварыПереданныеСписываемыеНаКонецПериода
		|ИЗ
		|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(&КонГраница,
		|		АналитикаУчетаНоменклатуры.Склад ССЫЛКА Справочник.Организации
		|		//&УсловиеОтбора2
		|	) КАК ТоварыПереданные
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Склад,
		|	ТоварыПереданные.ВидЗапасов.Организация,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Номенклатура,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Характеристика,
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Назначение
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТоварыПереданные.КоличествоОстаток) <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Комиссионер,
		|	Комитент,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////";
		
		ТекстЗапросаТоваровКОформлению = ?(ЗначениеЗаполнено(ТекстЗапросаТоваровКОформлению),
										ТекстЗапросаТоваровКОформлению + ТекстОбъединить,
										ТекстЗапросаТоваровКОформлению);
		
		ТекстЗапросаТоваровКОформлению = ТекстЗапросаТоваровКОформлению + "
		|ВЫБРАТЬ //&КоличествоЗаписей
		|	ТоварыКомитенту.Валюта                     КАК Валюта,
		|	ТоварыКомитенту.Комиссионер                КАК Комиссионер,
		|	ТоварыКомитенту.Комитент                   КАК Комитент,
		|	ТоварыКомитенту.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТоварыКомитенту.ВидЗапасов                 КАК ВидЗапасов,
		|	0                                          КАК Остаток,
		|	ВЫБОР
		|		КОГДА ТоварыКомитенту.Списано <= ТоварыПереданные.Списано
		|			ТОГДА ТоварыКомитенту.Списано
		|		КОГДА ТоварыКомитенту.Списано > ТоварыПереданные.Списано
		|			ТОГДА ТоварыПереданные.Списано
		|	КОНЕЦ                                      КАК Списано,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациямиОСписании) КАК ХозяйственнаяОперация
		|ИЗ
		|	ТоварыКомитентуСписываемыеНаКонецПериода КАК ТоварыКомитенту
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыПереданныеСписываемыеНаКонецПериода КАК ТоварыПереданные
		|		ПО ТоварыКомитенту.Комиссионер = ТоварыПереданные.Комиссионер
		|			И ТоварыКомитенту.Комитент = ТоварыПереданные.Комитент
		|			И ТоварыКомитенту.Номенклатура = ТоварыПереданные.Номенклатура
		|			И ТоварыКомитенту.Характеристика = ТоварыПереданные.Характеристика
		|			И ТоварыКомитенту.Назначение = ТоварыПереданные.Назначение
		|
		|ГДЕ
		|	ВЫБОР
		|		КОГДА ТоварыКомитенту.Списано <= ТоварыПереданные.Списано
		|			ТОГДА ТоварыКомитенту.Списано
		|		КОГДА ТоварыКомитенту.Списано > ТоварыПереданные.Списано
		|			ТОГДА ТоварыПереданные.Списано
		|	КОНЕЦ <> 0
		|";
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекстЗапросаТоваровКОформлению) Тогда
		Возврат ТекстЗапросаТоваровКОформлению;
	Иначе
		
		ТекстЗапроса = ТекстЗапросаВременныхТаблиц + "
		|ВЫБРАТЬ //&КоличествоЗаписей
		|	КОформлению.Валюта                     КАК Валюта,
		|	КОформлению.Комиссионер                КАК Комиссионер,
		|	КОформлению.Комитент                   КАК Комитент,
		|	КОформлению.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	СУММА(КОформлению.Остаток)             КАК КоличествоОстаток,
		|	СУММА(КОформлению.Списано)             КАК КоличествоСписано,
		|	КОформлению.ХозяйственнаяОперация      КАК ХозяйственнаяОперация,
		|	ВЫБОР
		|		КОГДА КОформлению.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациями)
		|			ТОГДА &ТекстОтчетОПродажах
		|		КОГДА КОформлению.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациямиОСписании)
		|			ТОГДА &ТекстОтчетОСписании
		|	КОНЕЦ                                  КАК ТипДокумента,
		|	ВидыЗапасов.Договор                    КАК Договор,
		|	ВидыЗапасов.ВидЦены                    КАК ВидЦены,
		|	ВидыЗапасов.НалогообложениеНДС         КАК НалогообложениеНДС
		|//&ИмяВременнойТаблицы
		|ИЗ
		|	ТекстЗапросаТоварыКОформлениюНаКонецПериода КАК КОформлению
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = КОформлению.ВидЗапасов
		|
		|СГРУППИРОВАТЬ ПО
		|	КОформлению.Валюта,
		|	КОформлению.Комиссионер,
		|	КОформлению.Комитент,
		|	КОформлению.АналитикаУчетаНоменклатуры,
		|	КОформлению.ХозяйственнаяОперация,
		|	ВидыЗапасов.Договор,
		|	ВидыЗапасов.ВидЦены,
		|	ВидыЗапасов.НалогообложениеНДС,
		|	ВЫБОР
		|		КОГДА КОформлению.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациями)
		|			ТОГДА &ТекстОтчетОПродажах
		|		КОГДА КОформлению.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациямиОСписании)
		|			ТОГДА &ТекстОтчетОСписании
		|	КОНЕЦ";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"ТекстЗапросаТоварыКОформлениюНаКонецПериода",
			"(" + ТекстЗапросаТоваровКОформлению + ")");
		
		ТекстУсловияЗапросаТоваровКОформлению = "";
		ТекстУсловияЗапросаТоваровПереданных  = "";
		ТекстУсловияЗапросаУслугКОформлению   = "";
		
		Если ПараметрыЗапроса.Свойство("ЭтоРасчетГиперссылки") Тогда
			
			Если ЗначениеЗаполнено(ПараметрыЗапроса.Комитент) Тогда
				ТекстУсловияЗапросаТоваровКОформлению = "
					|	И ВидЗапасов.ВладелецТовара = &Комитент";
				
				ТекстУсловияЗапросаТоваровПереданных = "
					|	И ВидЗапасов.Организация = &Комитент";
				
				ТекстУсловияЗапросаУслугКОформлению = "
					|	И АналитикаУчетаНоменклатуры.Склад = &Комитент";
			КонецЕсли;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КонГраница", "");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//&ИмяВременнойТаблицы", "");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//&КоличествоЗаписей", "ПЕРВЫЕ 1");
			
		Иначе
			
			ТекстЗапросаВременныхТаблиц = СтрЗаменить(ТекстЗапросаВременныхТаблиц, "&КонГраница", "&НачГраница");
			ТекстЗапросаВременныхТаблиц = СтрЗаменить(ТекстЗапросаВременныхТаблиц, "НаКонецПериода", "НаНачалоПериода");
			
			ТекстЗапроса = ТекстЗапроса + ТекстРазделить + ТекстЗапросаВременныхТаблиц + "
			|ВЫБРАТЬ
			|	КОформлению.Валюта                     КАК Валюта,
			|	КОформлению.Комиссионер                КАК Комиссионер,
			|	КОформлению.Комитент                   КАК Комитент,
			|	КОформлению.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	СУММА(КОформлению.Остаток)             КАК КоличествоОстаток,
			|	СУММА(КОформлению.Списано)             КАК КоличествоСписано,
			|	КОформлению.ХозяйственнаяОперация      КАК ХозяйственнаяОперация
			|ПОМЕСТИТЬ ТоварыКОформлениюНаНачалоПериода
			|ИЗ
			|	ТекстЗапросаТоварыКОформлениюНаНачалоПериода КАК КОформлению
			|
			|СГРУППИРОВАТЬ ПО
			|	КОформлению.Валюта,
			|	КОформлению.Комиссионер,
			|	КОформлению.Комитент,
			|	КОформлению.АналитикаУчетаНоменклатуры,
			|	КОформлению.ХозяйственнаяОперация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Валюта,
			|	Комиссионер,
			|	Комитент,
			|	АналитикаУчетаНоменклатуры,
			|	ХозяйственнаяОперация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОстатокТоваровНаКонецПериода.Валюта КАК Валюта,
			|	ОстатокТоваровНаКонецПериода.Комиссионер КАК Комиссионер,
			|	ОстатокТоваровНаКонецПериода.Комитент КАК Комитент,
			|	СУММА(ОстатокТоваровНаКонецПериода.КоличествоОстаток) - СУММА(ЕСТЬNULL(ОстатокТоваровНаНачалоПериода.КоличествоОстаток, 0)) КАК КоличествоОстаток,
			|	СУММА(ОстатокТоваровНаКонецПериода.КоличествоСписано) - СУММА(ЕСТЬNULL(ОстатокТоваровНаНачалоПериода.КоличествоСписано, 0)) КАК КоличествоСписано,
			|	ОстатокТоваровНаКонецПериода.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
			|	ОстатокТоваровНаКонецПериода.ТипДокумента КАК ТипДокумента,
			|	ОстатокТоваровНаКонецПериода.Договор КАК Договор,
			|	ОстатокТоваровНаКонецПериода.ВидЦены КАК ВидЦены,
			|	ОстатокТоваровНаКонецПериода.НалогообложениеНДС КАК НалогообложениеНДС
			|ИЗ
			|	ТоварыКОформлениюНаКонецПериода КАК ОстатокТоваровНаКонецПериода
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыКОформлениюНаНачалоПериода КАК ОстатокТоваровНаНачалоПериода
			|		ПО ОстатокТоваровНаКонецПериода.Валюта = ОстатокТоваровНаНачалоПериода.Валюта
			|			И ОстатокТоваровНаКонецПериода.Комиссионер = ОстатокТоваровНаНачалоПериода.Комиссионер
			|			И ОстатокТоваровНаКонецПериода.Комитент = ОстатокТоваровНаНачалоПериода.Комитент
			|			И ОстатокТоваровНаКонецПериода.АналитикаУчетаНоменклатуры = ОстатокТоваровНаНачалоПериода.АналитикаУчетаНоменклатуры
			|			И ОстатокТоваровНаКонецПериода.ХозяйственнаяОперация = ОстатокТоваровНаНачалоПериода.ХозяйственнаяОперация
			|
			|ГДЕ
			|	ОстатокТоваровНаКонецПериода.КоличествоОстаток - ЕСТЬNULL(ОстатокТоваровНаНачалоПериода.КоличествоОстаток, 0) <> 0
			|	ИЛИ ОстатокТоваровНаКонецПериода.КоличествоСписано - ЕСТЬNULL(ОстатокТоваровНаНачалоПериода.КоличествоСписано, 0) <> 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ОстатокТоваровНаКонецПериода.Валюта,
			|	ОстатокТоваровНаКонецПериода.Комиссионер,
			|	ОстатокТоваровНаКонецПериода.Комитент,
			|	ОстатокТоваровНаКонецПериода.ХозяйственнаяОперация,
			|	ОстатокТоваровНаКонецПериода.ТипДокумента,
			|	ОстатокТоваровНаКонецПериода.Договор,
			|	ОстатокТоваровНаКонецПериода.ВидЦены,
			|	ОстатокТоваровНаКонецПериода.НалогообложениеНДС";
			
			ТекстПоместить = "ПОМЕСТИТЬ ТоварыКОформлениюНаКонецПериода";
			ТекстЗапросаТоваровКОформлению = СтрЗаменить(ТекстЗапросаТоваровКОформлению, "&КонГраница", "&НачГраница");
			ТекстЗапросаТоваровКОформлению = СтрЗаменить(ТекстЗапросаТоваровКОформлению, "НаКонецПериода", "НаНачалоПериода");
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//&ИмяВременнойТаблицы", ТекстПоместить);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"ТекстЗапросаТоварыКОформлениюНаНачалоПериода",
				"(" + ТекстЗапросаТоваровКОформлению + ")");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//&КоличествоЗаписей", "");
			
			Если ПараметрыЗапроса.Свойство("Комиссионер")
				И ЗначениеЗаполнено(ПараметрыЗапроса.Комиссионер)
				И ЗначениеЗаполнено(ПараметрыЗапроса.Комитент)
				И ПараметрыЗапроса.Комиссионер = ПараметрыЗапроса.Комитент Тогда
				
				// Условия формирования списка при открытии из обработки "ОпперацииЗакрытияМесяца".
				
				ТекстУсловияЗапросаТоваровКОформлению = "
					|	И (ВидЗапасов.ВладелецТовара = &Комитент
					|		ИЛИ ВидЗапасов.Организация = &Комиссионер)";
				
				ТекстУсловияЗапросаТоваровПереданных = "
					|	И (ВидЗапасов.Организация = &Комитент
					|		ИЛИ АналитикаУчетаНоменклатуры.Склад = &Комиссионер)";
				
				ТекстУсловияЗапросаУслугКОформлению = "
					|	И (АналитикаУчетаНоменклатуры.Склад = &Комитент
					|		ИЛИ Организация = &Комиссионер)";
				
			Иначе
				
				Если ЗначениеЗаполнено(ПараметрыЗапроса.Комитент) Тогда
					УсловиеЗапросаТоваровКОформлению = "
						|	И &Комитент В (ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка), ВидЗапасов.ВладелецТовара)";
					
					ТекстУсловияЗапросаТоваровКОформлению = ТекстУсловияЗапросаТоваровКОформлению + УсловиеЗапросаТоваровКОформлению;
					
					УсловиеЗапросаТоваровПереданных = "
						|	И &Комитент В (ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка), ВидЗапасов.Организация)";
					
					ТекстУсловияЗапросаТоваровПереданных = ТекстУсловияЗапросаТоваровПереданных + УсловиеЗапросаТоваровПереданных;
					
					УсловиеЗапросаУслугКОформлению = "
						|	И &Комитент В (ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка), АналитикаУчетаНоменклатуры.Склад)";
					
					ТекстУсловияЗапросаУслугКОформлению = ТекстУсловияЗапросаУслугКОформлению + УсловиеЗапросаУслугКОформлению;
				КонецЕсли;
				
				Если ПараметрыЗапроса.Свойство("Комиссионер")
					И ЗначениеЗаполнено(ПараметрыЗапроса.Комиссионер) Тогда
					
					УсловиеЗапросаТоваровКОформлению = "
						|	И &Комиссионер В (ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка), ВидЗапасов.Организация)";
					
					ТекстУсловияЗапросаТоваровКОформлению = ТекстУсловияЗапросаТоваровКОформлению + УсловиеЗапросаТоваровКОформлению;
					
					УсловиеЗапросаТоваровПереданных = "
						|	И &Комиссионер В (ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка), АналитикаУчетаНоменклатуры.Склад)";
					
					ТекстУсловияЗапросаТоваровПереданных = ТекстУсловияЗапросаТоваровПереданных + УсловиеЗапросаТоваровПереданных;
					
					УсловиеЗапросаУслугКОформлению = "
						|	И &Комиссионер В (ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка), Организация)";
					
					ТекстУсловияЗапросаУслугКОформлению = ТекстУсловияЗапросаУслугКОформлению + УсловиеЗапросаУслугКОформлению;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//&УсловиеОтбора1",
			?(ЗначениеЗаполнено(ТекстУсловияЗапросаТоваровКОформлению), ТекстУсловияЗапросаТоваровКОформлению, ""));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//&УсловиеОтбора2",
			?(ЗначениеЗаполнено(ТекстУсловияЗапросаТоваровПереданных), ТекстУсловияЗапросаТоваровПереданных, ""));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//&УсловиеОтбора3",
			?(ЗначениеЗаполнено(ТекстУсловияЗапросаУслугКОформлению), ТекстУсловияЗапросаУслугКОформлению, ""));
		
		Возврат ТекстЗапроса;
		
	КонецЕсли;
	
КонецФункции

// Возвращает текст гиперссылки перехода из журнала документов в рабочее место оформления отчетов по комиссии.
//
// Параметры:
//	Параметры - Структура - параметры формирования текста гиперссылки.
//
// ВозвращаемоеЗначение:
//	ФорматированнаяСтрока, Неопределено - текст гиперссылки перехода в рабочее место оформления отчетов по комиссии.
//
Функция СформироватьГиперссылкуКОформлению(Параметры) Экспорт
	
	ХозяйственныеОперацииИДокументы = Параметры.ХозяйственныеОперацииИДокументы;
	
	Отбор = Новый Структура("Отбор, КлючНазначенияИспользования", Истина, "ОтчетПоКомиссииМеждуОрганизациями");
	ХозяйственныеОперацииОтчетов = ХозяйственныеОперацииИДокументы.НайтиСтроки(Отбор);
	
	Если ХозяйственныеОперацииОтчетов.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Отбор = Новый Структура("Отбор, ПолноеИмяДокумента", Истина, "Документ.ОтчетПоКомиссииМеждуОрганизациями");
	ОформитьПоОтчетамКомиссии = ХозяйственныеОперацииИДокументы.НайтиСтроки(Отбор).Количество() > 0;
	
	Отбор = Новый Структура("Отбор, ПолноеИмяДокумента", Истина, "Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании");
	ОтобратьПоОтчетамКомиссииОСписании = ХозяйственныеОперацииИДокументы.НайтиСтроки(Отбор).Количество() > 0;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Комитент",                           Параметры.Организация);
	ПараметрыЗапроса.Вставить("ЭтоРасчетГиперссылки",               Параметры.ЭтоРасчетГиперссылки);
	ПараметрыЗапроса.Вставить("ОформитьПоОтчетамКомиссии",          ОформитьПоОтчетамКомиссии);
	ПараметрыЗапроса.Вставить("ОтобратьПоОтчетамКомиссииОСписании", ОтобратьПоОтчетамКомиссииОСписании);
	
	ТекстЗапроса = ТекстЗапросаОформляемыеОтчетыПоКомиссии(ПараметрыЗапроса);
	
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	ТекстГиперссылки    = НСтр("ru = 'Отчеты по комиссии'");
	ТекстОтчетОПродажах = НСтр("ru='Отчет о продажах'");
	ТекстОтчетОСписании = НСтр("ru='Отчет о списании'");
	
	Запрос.УстановитьПараметр("Комитент",            Параметры.Организация);
	Запрос.УстановитьПараметр("ТекстОтчетОПродажах", ТекстОтчетОПродажах);
	Запрос.УстановитьПараметр("ТекстОтчетОСписании", ТекстОтчетОСписании);
	
	Если Не Запрос.Выполнить().Пустой() Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки, , , ,
			"Обработка.ЖурналДокументовИнтеркампани.Форма.РабочееМестоОтчетыПоКомиссии");
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки, , ЦветаСтиля.НезаполненноеПолеТаблицы, ,
			"Обработка.ЖурналДокументовИнтеркампани.Форма.РабочееМестоОтчетыПоКомиссии");
	КонецЕсли;
	
КонецФункции

Функция СформироватьГиперссылкуСмТакжеВРаботе(Параметры) Экспорт
	ХозяйственныеОперацииИДокументы = ХозяйственныеОперацииИДокументы();
	НайденныеСтроки = ХозяйственныеОперацииИДокументы.НайтиСтроки(
		Новый Структура("КлючНазначенияИспользования", Параметры.КлючНазначенияИспользования));
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выводить = Ложь;
	
	//Все строки с одинаковым ключом должны иметь одинаковый заголовок рабочего места 
	СтрокаПоКлючуНазначенияИспользования = НайденныеСтроки[0];
	
	Возврат Новый ФорматированнаяСтрока(СтрокаПоКлючуНазначенияИспользования.ЗаголовокРабочегоМеста, , , ,
				"Обработка.ЖурналДокументовИнтеркампани.Форма.ФормаСписка");
КонецФункции

#КонецОбласти

Функция ХозяйственныеОперацииИДокументы()
	
	Перем Строка, СтрокаВозвратТоваровМеждуОрганизациями, СтрокаОтчетПоКомиссииМеждуОрганизациями, СтрокаОтчетПоКомиссииМеждуОрганизациямиОСписании, СтрокаПередачаТоваровМеждуОрганизациями, ХозяйственныеОперацииИДокументы;
	
	ХозяйственныеОперацииИДокументы = ОбщегоНазначенияУТ.НоваяТаблицаХозяйственныеОперацииИДокументы();
	
	// ВозвратТоваровМеждуОрганизациями
	
	СтрокаВозвратТоваровМеждуОрганизациями = ХозяйственныеОперацииИДокументы.Добавить();
	
	Строка = СтрокаВозвратТоваровМеждуОрганизациями;
	Строка.КлючНазначенияИспользования = "ВозвратТоваровМеждуОрганизациями";
	Строка.ХозяйственнаяОперация       = Перечисления.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями;
	Строка.ДобавитьКнопкуСоздать       = Истина;
	Строка.ПолноеИмяДокумента          = "Документ.ВозвратТоваровМеждуОрганизациями";
	Строка.ЗаголовокРабочегоМеста      = НСтр("ru = 'Документы между организациями (возвраты)'");
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаВозвратТоваровМеждуОрганизациями);
	Строка.ХозяйственнаяОперация       = Перечисления.ХозяйственныеОперации.ВозвратПоКомиссииМеждуОрганизациями;
	
	// ОтчетПоКомиссииМеждуОрганизациями
	
	СтрокаОтчетПоКомиссииМеждуОрганизациями = ХозяйственныеОперацииИДокументы.Добавить();
	
	Строка = СтрокаОтчетПоКомиссииМеждуОрганизациями;
	Строка.КлючНазначенияИспользования = "ОтчетПоКомиссииМеждуОрганизациями";
	Строка.ХозяйственнаяОперация       = Перечисления.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациями;
	Строка.ДобавитьКнопкуСоздать       = Истина;
	Строка.ПолноеИмяДокумента          = "Документ.ОтчетПоКомиссииМеждуОрганизациями";
	Строка.ЗаголовокРабочегоМеста      = НСтр("ru = 'Документы между организациями (отчеты по комиссии)'");
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовИнтеркампани";
	
	// ОтчетПоКомиссииМеждуОрганизациямиОСписании
	
	СтрокаОтчетПоКомиссииМеждуОрганизациямиОСписании = ХозяйственныеОперацииИДокументы.Добавить();
	
	Строка = СтрокаОтчетПоКомиссииМеждуОрганизациямиОСписании;
	Строка.КлючНазначенияИспользования = "ОтчетПоКомиссииМеждуОрганизациями";
	Строка.ХозяйственнаяОперация       = Перечисления.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациямиОСписании;
	Строка.ДобавитьКнопкуСоздать       = Истина;
	Строка.ПолноеИмяДокумента          = "Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании";
	Строка.ЗаголовокРабочегоМеста      = НСтр("ru = 'Документы между организациями (отчеты по комиссии о списании)'");
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Обработка.ЖурналДокументовИнтеркампани";
	
	// ПередачаТоваровМеждуОрганизациями
	
	СтрокаПередачаТоваровМеждуОрганизациями = ХозяйственныеОперацииИДокументы.Добавить();
	
	Строка = СтрокаПередачаТоваровМеждуОрганизациями;
	Строка.КлючНазначенияИспользования = "ПередачаТоваровМеждуОрганизациями";
	Строка.ХозяйственнаяОперация       = Перечисления.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию;
	Строка.ДобавитьКнопкуСоздать       = Истина;
	Строка.ПолноеИмяДокумента          = "Документ.ПередачаТоваровМеждуОрганизациями";
	Строка.ЗаголовокРабочегоМеста      = НСтр("ru = 'Документы между организациями (передачи)'");
	Строка.МенеджерРасчетаГиперссылкиКОформлению = "Документ.ПередачаТоваровМеждуОрганизациями";
	
	Строка = ХозяйственныеОперацииИДокументы.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, СтрокаПередачаТоваровМеждуОрганизациями);
	Строка.ХозяйственнаяОперация       = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию;
	
	Возврат ХозяйственныеОперацииИДокументы;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриОпределенииКомандПодключенныхКОбъекту(Команды) Экспорт
	
КонецПроцедуры

Процедура ВзвестиКонстантуПериодыРезервовТоваровОрганизацийАктуальны()
	
	УстановитьПривилегированныйРежим(Истина);
	Константы.ПериодыРезервовТоваровОрганизацийАктуальны.Установить(Истина);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция РезультатФоновыхЗаданий()
	                                       
	Результат = Новый Структура();
	Результат.Вставить("ЛишниеРезервы");
	Результат.Вставить("КОформлениюЗаПериод");
	Результат.Вставить("ЕстьТоварыКОформлениюПередачПередВыкупом");
	
	Возврат Результат;
	
КонецФункции

Функция ЕстьЛишниеРезервы() Экспорт
	
	ДатаПервогоРезерва = ЗапасыСервер.ДатаПервогоРезерва(Неопределено);
	
	Если Не ЗначениеЗаполнено(ДатаПервогоРезерва) Тогда
		ВзвестиКонстантуПериодыРезервовТоваровОрганизацийАктуальны();
		Возврат "НетЛишнихРезервов";
	КонецЕсли;
	
	Если Константы.ПериодыРезервовТоваровОрганизацийАктуальны.Получить() Тогда
		Возврат "НетЛишнихРезервов";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(РезервыПоПериодам.Период, МЕСЯЦ) КАК Период,
	|	РезервыПоПериодам.Организация КАК Организация,
	|	РезервыПоПериодам.ВидЗапасов КАК ВидЗапасов,
	|	РезервыПоПериодам.НомерГТД КАК НомерГТД,
	|	РезервыПоПериодам.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	РезервыПоПериодам.КоличествоОборот КАК РезервВПериоде
	|ПОМЕСТИТЬ РезервыПоПериодам
	|ИЗ
	|	РегистрНакопления.РезервыТоваровОрганизаций.ОстаткиИОбороты(&НачалоПериода, , МЕСЯЦ, Движения, ) КАК РезервыПоПериодам
	|ГДЕ
	|	РезервыПоПериодам.КоличествоОборот > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ВидЗапасов КАК ВидЗапасов,
	|	ВложенныйЗапрос.НомерГТД КАК НомерГТД,
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	СУММА(ВложенныйЗапрос.КоличествоКПередаче) КАК КоличествоКПередаче,
	|	СУММА(ВложенныйЗапрос.Резерв) КАК Резерв
	|ИЗ
	|	(ВЫБРАТЬ
	|		КОНЕЦПЕРИОДА(ТоварыОрганизацийОстатки.Период, МЕСЯЦ) КАК Период,
	|		ТоварыОрганизацийОстатки.Организация КАК Организация,
	|		ТоварыОрганизацийОстатки.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыОрганизацийОстатки.НомерГТД КАК НомерГТД,
	|		ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ВЫБОР
	|			КОГДА ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток >= 0
	|				ТОГДА 0
	|			КОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток >= 0
	|				ТОГДА -ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток
	|			КОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток - ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток > 0
	|				ТОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток - ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КоличествоКПередаче,
	|		0 КАК Резерв
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(
	|				&НачалоПериода,
	|				,
	|				МЕСЯЦ,
	|				Движения,
	|				(Организация, ВидЗапасов, НомерГТД, АналитикаУчетаНоменклатуры) В
	|					(ВЫБРАТЬ
	|						РезервыПоПериодам.Организация КАК Организация,
	|						РезервыПоПериодам.ВидЗапасов КАК ВидЗапасов,
	|						РезервыПоПериодам.НомерГТД КАК НомерГТД,
	|						РезервыПоПериодам.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|					ИЗ
	|						РезервыПоПериодам КАК РезервыПоПериодам)) КАК ТоварыОрганизацийОстатки
	|	ГДЕ
	|		ВЫБОР
	|				КОГДА ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток >= 0
	|					ТОГДА 0
	|				КОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток >= 0
	|					ТОГДА -ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток
	|				КОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток - ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток > 0
	|					ТОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток - ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток
	|				ИНАЧЕ 0
	|			КОНЕЦ > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыПоПериодам.Период,
	|		РезервыПоПериодам.Организация,
	|		РезервыПоПериодам.ВидЗапасов,
	|		РезервыПоПериодам.НомерГТД,
	|		РезервыПоПериодам.АналитикаУчетаНоменклатуры,
	|		0,
	|		РезервыПоПериодам.РезервВПериоде
	|	ИЗ
	|		РезервыПоПериодам КАК РезервыПоПериодам) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ВидЗапасов,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.КоличествоКПередаче) < СУММА(ВложенныйЗапрос.Резерв)
	|	И СУММА(ВложенныйЗапрос.Резерв) > 0 ";

		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ДатаПервогоРезерва));
	
	Если Запрос.Выполнить().Пустой() Тогда
		ВзвестиКонстантуПериодыРезервовТоваровОрганизацийАктуальны();
		Возврат "НетЛишнихРезервов";
	Иначе
		Возврат "ЕстьЛишниеРезервы";
	КонецЕсли;
	
КонецФункции

Функция ЕстьТоварыКОформлениюПередачПередВыкупом(Параметры)
	
	Если Параметры.Свойство("ОформлятьВыкупы")
		И Параметры.ОформлятьВыкупы Тогда
		
		Запрос = Новый Запрос;
		ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	РезервыТоваровОрганизаций.Организация КАК Организация,
		|	РезервыТоваровОрганизаций.ВидЗапасов КАК ВидЗапасов,
		|	РезервыТоваровОрганизаций.НомерГТД КАК НомерГТД,
		|	СУММА(РезервыТоваровОрганизаций.Количество) КАК Количество
		|ИЗ
		|	РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
		|ГДЕ
		|	РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И РезервыТоваровОрганизаций.Организация <> РезервыТоваровОрганизаций.КорОрганизация
		|	И РезервыТоваровОрганизаций.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
		|	И &Склад
		|	И &ВладелецТовара
		|	И &ОрганизацияРезерва
		|
		|СГРУППИРОВАТЬ ПО
		|	РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
		|	РезервыТоваровОрганизаций.НомерГТД,
		|	РезервыТоваровОрганизаций.Организация,
		|	РезервыТоваровОрганизаций.ВидЗапасов
		|
		|ИМЕЮЩИЕ
		|	СУММА(РезервыТоваровОрганизаций.Количество) > 0";
		
		Если ЗначениеЗаполнено(Параметры.Получатель) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОрганизацияРезерва", "РезервыТоваровОрганизаций.Организация = &Организация");
			
			Запрос.УстановитьПараметр("Организация", Параметры.Получатель);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОрганизацияРезерва", "ИСТИНА");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Параметры.Отправитель) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВладелецТовара", "РезервыТоваровОрганизаций.ВидЗапасов.ВладелецТовара = &ВладелецТовара");
			
			Запрос.УстановитьПараметр("ВладелецТовара", Параметры.Отправитель);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВладелецТовара", "ИСТИНА");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Параметры.Склад) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Склад", "РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры.Склад = &Склад");
			
			Запрос.УстановитьПараметр("Склад", Параметры.Склад);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Склад", "ИСТИНА");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		ЕстьТоварыКОформлениюПередачПередВыкупом = Не Запрос.Выполнить().Пустой();
		
	Иначе
		ЕстьТоварыКОформлениюПередачПередВыкупом = Ложь;
	КонецЕсли;

	Возврат ЕстьТоварыКОформлениюПередачПередВыкупом;
	
КонецФункции

Процедура РаспределитьРезервыПоПериодам(Параметры, АдресВременногоХранилища) Экспорт

	ДатаПервогоРезерва = ЗапасыСервер.ДатаПервогоРезерва(Неопределено);
	
	Если Не ЗначениеЗаполнено(ДатаПервогоРезерва) Тогда
		ВзвестиКонстантуПериодыРезервовТоваровОрганизацийАктуальны();
		ПоместитьВоВременноеХранилище("НетЛишнихРезервов", АдресВременногоХранилища);
		Возврат;
	КонецЕсли;
	
	ДатаПервогоРезерва = КонецМесяца(ДатаПервогоРезерва);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РезервыТоваровОрганизаций.Период КАК Период
	|ИЗ
	|	РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	ДатаПоследнегоРезерва = КонецМесяца(Выборка.Период);
	
	ТекущийМесяц = ДатаПервогоРезерва;
	
	Отказ = Ложь;
	
	Пока ТекущийМесяц <= ДатаПоследнегоРезерва Цикл
		
		Если Не РаспределитьРезервыВПериоде(ТекущийМесяц) Тогда
			Отказ = Истина;
			Прервать;
		КонецЕсли;
		
		ТекущийМесяц = КонецМесяца(ТекущийМесяц + 1);
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РезервыТоваровОрганизаций.Период КАК Период
		|ИЗ
		|	РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Выборка.Следующий();
		
		ДатаПоследнегоРезерва = КонецМесяца(Выборка.Период);
	КонецЦикла;
	
	Если Не Отказ Тогда
		ЗаполнитьКОформлениюЗаПериод(Параметры, АдресВременногоХранилища);
	Иначе
		Результат = РезультатФоновыхЗаданий();
		Результат.ЛишниеРезервы                            = "Ошибка";
		Результат.КОформлениюЗаПериод                      = Неопределено;
		Результат.ЕстьТоварыКОформлениюПередачПередВыкупом = Ложь;
		ПоместитьВоВременноеХранилище(Результат, АдресВременногоХранилища);
	КонецЕсли;	
	
КонецПроцедуры

Функция РаспределитьРезервыВПериоде(ТекущийМесяц)
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.РезервыТоваровОрганизаций");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.ТоварыОрганизаций");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		
		БлокировкаДанных.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НачалоПериодаГраница", Новый Граница(НачалоМесяца(ТекущийМесяц),ВидГраницы.Включая));
		Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецМесяца(ТекущийМесяц),ВидГраницы.Включая));
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ТекущийМесяц));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ТекущийМесяц));
		
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	КОНЕЦПЕРИОДА(РезервыПоПериодам.Период, МЕСЯЦ) КАК Период,
		|	РезервыПоПериодам.Организация КАК Организация,
		|	РезервыПоПериодам.ВидЗапасов КАК ВидЗапасов,
		|	РезервыПоПериодам.НомерГТД КАК НомерГТД,
		|	РезервыПоПериодам.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	-РезервыПоПериодам.КоличествоОборот КАК Количество
		|ПОМЕСТИТЬ ДвиженияДокумента
		|ИЗ
		|	РегистрНакопления.РезервыТоваровОрганизаций.ОстаткиИОбороты(&НачалоПериодаГраница, &КонецПериодаГраница, МЕСЯЦ, Движения, ) КАК РезервыПоПериодам
		|ГДЕ
		|	РезервыПоПериодам.КоличествоОборот > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОНЕЦПЕРИОДА(ТоварыОрганизацийОстатки.Период, МЕСЯЦ) КАК Период,
		|	ТоварыОрганизацийОстатки.Организация КАК Организация,
		|	ТоварыОрганизацийОстатки.ВидЗапасов КАК ВидЗапасов,
		|	ТоварыОрганизацийОстатки.НомерГТД КАК НомерГТД,
		|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ВЫБОР
		|		КОГДА ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток >= 0
		|			ТОГДА 0
		|		КОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток >= 0
		|			ТОГДА -ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток
		|		КОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток - ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток > 0
		|			ТОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток - ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоКПередаче
		|ПОМЕСТИТЬ ВТКоличествоКПередаче
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(
		|			&НачалоПериодаГраница,
		|			&КонецПериодаГраница,
		|			МЕСЯЦ,
		|			Движения,
		|			(Организация, ВидЗапасов, НомерГТД, АналитикаУчетаНоменклатуры) В
		|				(ВЫБРАТЬ
		|					ДвиженияДокумента.Организация КАК Организация,
		|					ДвиженияДокумента.ВидЗапасов КАК ВидЗапасов,
		|					ДвиженияДокумента.НомерГТД КАК НомерГТД,
		|					ДвиженияДокумента.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|				ИЗ
		|					ДвиженияДокумента КАК ДвиженияДокумента)) КАК ТоварыОрганизацийОстатки
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток >= 0
		|				ТОГДА 0
		|			КОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток >= 0
		|				ТОГДА -ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток
		|			КОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток - ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток > 0
		|				ТОГДА ТоварыОрганизацийОстатки.КоличествоНачальныйОстаток - ТоварыОрганизацийОстатки.КоличествоКонечныйОстаток
		|			ИНАЧЕ 0
		|		КОНЕЦ > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.ВидЗапасов КАК ВидЗапасов,
		|	ВложенныйЗапрос.НомерГТД КАК НомерГТД,
		|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|ПОМЕСТИТЬ ВтОтборыОстатковОрганизаций
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТКоличествоКПередаче.Период КАК Период,
		|		ВТКоличествоКПередаче.Организация КАК Организация,
		|		ВТКоличествоКПередаче.ВидЗапасов КАК ВидЗапасов,
		|		ВТКоличествоКПередаче.НомерГТД КАК НомерГТД,
		|		ВТКоличествоКПередаче.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|		ВТКоличествоКПередаче.КоличествоКПередаче КАК КоличествоКПередаче,
		|		0 КАК Резерв
		|	ИЗ
		|		ВТКоличествоКПередаче КАК ВТКоличествоКПередаче
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДвиженияДокумента.Период,
		|		ДвиженияДокумента.Организация,
		|		ДвиженияДокумента.ВидЗапасов,
		|		ДвиженияДокумента.НомерГТД,
		|		ДвиженияДокумента.АналитикаУчетаНоменклатуры,
		|		0,
		|		-ДвиженияДокумента.Количество
		|	ИЗ
		|		ДвиженияДокумента КАК ДвиженияДокумента) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Период,
		|	ВложенныйЗапрос.Организация,
		|	ВложенныйЗапрос.ВидЗапасов,
		|	ВложенныйЗапрос.НомерГТД,
		|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВложенныйЗапрос.КоличествоКПередаче) < СУММА(ВложенныйЗапрос.Резерв) И
		|	СУММА(ВложенныйЗапрос.Резерв) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаНоменклатуры,
		|	ВидЗапасов,
		|	Организация,
		|	НомерГТД";
		
		Запрос.Текст = ТекстЗапроса;
		
		Результаты = Запрос.ВыполнитьПакет();
		//
		//Если Результаты[1].Выгрузить()[0].Количество = 0 Тогда
		//	ОтменитьТранзакцию();
		//	Возврат Истина;
		//КонецЕсли;
		
		ТекстЗапроса =
		"
		|	//%ТекстВТОстаткиПоМесяцам%
		|	
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СгруппированныеОстатки.Период КАК Период,
		|	СгруппированныеОстатки.Организация КАК Организация,
		|	СгруппированныеОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	СгруппированныеОстатки.ВидЗапасов КАК ВидЗапасов,
		|	СгруппированныеОстатки.НомерГТД КАК НомерГТД,
		|	МИНИМУМ(СгруппированныеОстатки.КоличествоОстаток) КАК КоличествоОстаток,
		|	СУММА(СгруппированныеОстатки.СчетчикПериодов) КАК СчетчикПериодов
		|ПОМЕСТИТЬ СгруппированныеОстатки
		|ИЗ
		|	(//%ТекстОстаткиПоМесяцам%)  КАК СгруппированныеОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Период,
		|	НомерПериода,
		|	Организация,
		|	АналитикаУчетаНоменклатуры,
		|	ВидЗапасов,
		|	НомерГТД
		|
		|ИМЕЮЩИЕ
		|	(МИНИМУМ(СгруппированныеОстатки.КоличествоОстаток) <> 0
		|	И СУММА(СгруппированныеОстатки.СчетчикПериодов) >= &ВсегоПериодов - СгруппированныеОстатки.НомерПериода + 1)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаНоменклатуры,
		|	ВидЗапасов,
		|	НомерГТД,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОНЕЦПЕРИОДА(ТаблицаНовыеРезервы.Период, МЕСЯЦ) КАК Период,
		|	ТаблицаНовыеРезервы.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТаблицаНовыеРезервы.НомерГТД КАК НомерГТД,
		|	ТаблицаНовыеРезервы.Организация КАК Организация,
		|	ТаблицаНовыеРезервы.ВидЗапасов КАК ВидЗапасов,
		|	ТаблицаНовыеРезервы.КорОрганизация КАК КорОрганизация,
		|	ТаблицаНовыеРезервы.КорВидЗапасов КАК КорВидЗапасов,
		|	СУММА(ТаблицаНовыеРезервы.Количество) КАК Количество,
		|	ТаблицаНовыеРезервы.ЭтоСторно КАК ЭтоСторно,
		|	ТаблицаНовыеРезервы.ВидДвижения КАК ВидДвижения
		|ПОМЕСТИТЬ ВтИсходнаяТаблицаТаблицаНовыеРезервы
		|ИЗ
		|	РегистрНакопления.РезервыТоваровОрганизаций КАК ТаблицаНовыеРезервы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОтборыОстатковОрганизаций КАК ВтОтборыОстатковОрганизаций
		|		ПО ТаблицаНовыеРезервы.АналитикаУчетаНоменклатуры = ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры
		|			И ТаблицаНовыеРезервы.ВидЗапасов = ВтОтборыОстатковОрганизаций.ВидЗапасов
		|			И ТаблицаНовыеРезервы.Организация = ВтОтборыОстатковОрганизаций.Организация
		|			И ТаблицаНовыеРезервы.НомерГТД = ВтОтборыОстатковОрганизаций.НомерГТД
		|ГДЕ
		|	ТаблицаНовыеРезервы.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ ТаблицаНовыеРезервы.ЭтоСторно
		|	И ТаблицаНовыеРезервы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаНовыеРезервы.ЭтоСторно,
		|	ТаблицаНовыеРезервы.НомерГТД,
		|	ТаблицаНовыеРезервы.Организация,
		|	ТаблицаНовыеРезервы.АналитикаУчетаНоменклатуры,
		|	КОНЕЦПЕРИОДА(ТаблицаНовыеРезервы.Период, МЕСЯЦ),
		|	ТаблицаНовыеРезервы.ВидЗапасов,
		|	ТаблицаНовыеРезервы.ВидДвижения,
		|	ТаблицаНовыеРезервы.КорВидЗапасов,
		|	ТаблицаНовыеРезервы.КорОрганизация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КОНЕЦПЕРИОДА(ТаблицаНовыеРезервы.Период, МЕСЯЦ),
		|	ТаблицаНовыеРезервы.АналитикаУчетаНоменклатуры,
		|	ТаблицаНовыеРезервы.НомерГТД,
		|	ТаблицаНовыеРезервы.КорОрганизация,
		|	ТаблицаНовыеРезервы.КорВидЗапасов,
		|	ТаблицаНовыеРезервы.Организация,
		|	ТаблицаНовыеРезервы.ВидЗапасов,
		|	СУММА(ТаблицаНовыеРезервы.Количество),
		|	ТаблицаНовыеРезервы.ЭтоСторно,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|ИЗ
		|	РегистрНакопления.РезервыТоваровОрганизаций КАК ТаблицаНовыеРезервы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОтборыОстатковОрганизаций КАК ВтОтборыОстатковОрганизаций
		|		ПО ТаблицаНовыеРезервы.АналитикаУчетаНоменклатуры = ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры
		|			И ТаблицаНовыеРезервы.ВидЗапасов = ВтОтборыОстатковОрганизаций.ВидЗапасов
		|			И ТаблицаНовыеРезервы.Организация = ВтОтборыОстатковОрганизаций.Организация
		|			И ТаблицаНовыеРезервы.НомерГТД = ВтОтборыОстатковОрганизаций.НомерГТД
		|ГДЕ
		|	ТаблицаНовыеРезервы.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ ТаблицаНовыеРезервы.ЭтоСторно
		|	И ТаблицаНовыеРезервы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаНовыеРезервы.КорВидЗапасов,
		|	ТаблицаНовыеРезервы.НомерГТД,
		|	ТаблицаНовыеРезервы.ЭтоСторно,
		|	ТаблицаНовыеРезервы.КорОрганизация,
		|	ТаблицаНовыеРезервы.ВидДвижения,
		|	ТаблицаНовыеРезервы.АналитикаУчетаНоменклатуры,
		|	КОНЕЦПЕРИОДА(ТаблицаНовыеРезервы.Период, МЕСЯЦ),
		|	ТаблицаНовыеРезервы.Организация,
		|	ТаблицаНовыеРезервы.ВидЗапасов";
		
		ПараметрыДополненияЗапросаОстаткамиНаКаждыйМесяц = ЗапасыСервер.ПараметрыДополненияЗапросаОстаткамиНаКаждыйМесяц();
		ПараметрыДополненияЗапросаОстаткамиНаКаждыйМесяц.ЕстьОтборПоНомеруГТД = Истина;
		
		ВсегоПериодов = ЗапасыСервер.ДополнитьЗапросКонтролемОстатковНаКаждыйМесяц(Запрос, ТекстЗапроса, ПараметрыДополненияЗапросаОстаткамиНаКаждыйМесяц, ТекущийМесяц);
		Запрос.УстановитьПараметр("ВсегоПериодов", ВсегоПериодов);
		Запрос.Текст = ТекстЗапроса;
		
		РезультатыЗапроса = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
		
		ТаблицаНовыеРезервы = РезультатыЗапроса[РезультатыЗапроса.ВГраница()].Выгрузить();
		
		ТаблицаНовыеРезервы = ЗапасыСервер.РаспределитьРезервыТоваровОрганизацийПоПериодам(ВсегоПериодов, ТаблицаНовыеРезервы, МенеджерВременныхТаблиц);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаНовыеРезервы.Период КАК Период,
		|	ТаблицаНовыеРезервы.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТаблицаНовыеРезервы.Организация КАК Организация,
		|	ТаблицаНовыеРезервы.ВидЗапасов КАК ВидЗапасов,
		|	ТаблицаНовыеРезервы.НомерГТД КАК НомерГТД,
		|	ТаблицаНовыеРезервы.КорОрганизация КАК КорОрганизация,
		|	ТаблицаНовыеРезервы.КорВидЗапасов КАК КорВидЗапасов,
		|	ТаблицаНовыеРезервы.Количество КАК Количество,
		|	ТаблицаНовыеРезервы.ВидДвижения КАК ВидДвижения
		|ПОМЕСТИТЬ ВТТаблицаНовыеРезервы
		|ИЗ
		|	&ТаблицаНовыеРезервы КАК ТаблицаНовыеРезервы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Период КАК Период,
		|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.ВидЗапасов КАК ВидЗапасов,
		|	ВложенныйЗапрос.НомерГТД КАК НомерГТД,
		|	ВложенныйЗапрос.КорОрганизация КАК КорОрганизация,
		|	ВложенныйЗапрос.КорВидЗапасов КАК КорВидЗапасов,
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
		|	СУММА(ВложенныйЗапрос.Количество) < 0 КАК ЭтоСторно,
		|	ВложенныйЗапрос.ВидДвижения КАК ВидДвижения
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаНовыеРезервы.Период КАК Период,
		|		ТаблицаНовыеРезервы.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|		ТаблицаНовыеРезервы.Организация КАК Организация,
		|		ТаблицаНовыеРезервы.ВидЗапасов КАК ВидЗапасов,
		|		ТаблицаНовыеРезервы.НомерГТД КАК НомерГТД,
		|		ТаблицаНовыеРезервы.КорОрганизация КАК КорОрганизация,
		|		ТаблицаНовыеРезервы.КорВидЗапасов КАК КорВидЗапасов,
		|		ТаблицаНовыеРезервы.Количество КАК Количество,
		|		ТаблицаНовыеРезервы.ВидДвижения КАК ВидДвижения
		|	ИЗ
		|		ВТТаблицаНовыеРезервы КАК ТаблицаНовыеРезервы
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ИсходнаяТаблицаНовыеРезервы.Период,
		|		ИсходнаяТаблицаНовыеРезервы.АналитикаУчетаНоменклатуры,
		|		ИсходнаяТаблицаНовыеРезервы.Организация,
		|		ИсходнаяТаблицаНовыеРезервы.ВидЗапасов,
		|		ИсходнаяТаблицаНовыеРезервы.НомерГТД,
		|		ИсходнаяТаблицаНовыеРезервы.КорОрганизация,
		|		ИсходнаяТаблицаНовыеРезервы.КорВидЗапасов,
		|		-ИсходнаяТаблицаНовыеРезервы.Количество,
		|		ИсходнаяТаблицаНовыеРезервы.ВидДвижения
		|	ИЗ
		|		ВтИсходнаяТаблицаТаблицаНовыеРезервы КАК ИсходнаяТаблицаНовыеРезервы) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Период,
		|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры,
		|	ВложенныйЗапрос.Организация,
		|	ВложенныйЗапрос.ВидЗапасов,
		|	ВложенныйЗапрос.НомерГТД,
		|	ВложенныйЗапрос.КорОрганизация,
		|	ВложенныйЗапрос.КорВидЗапасов,
		|	ВложенныйЗапрос.ВидДвижения
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВложенныйЗапрос.Количество) <> 0";
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("ТаблицаНовыеРезервы", ТаблицаНовыеРезервы);
		
		ТаблицаНовыеРезервы = Запрос.Выполнить().Выгрузить();
		
		Если ТаблицаНовыеРезервы.Количество() > 0 Тогда
			
			КорректировкаРегистров = Документы.КорректировкаРегистров.СоздатьДокумент();
			КорректировкаРегистров.Дата = ТекущийМесяц;
			КорректировкаРегистров.Комментарий = НСтр("ru = 'Создан автоматически при распределении резервов товаров организаций по периодам.'",
			ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			
			НоваяСтрока = КорректировкаРегистров.ТаблицаРегистров.Добавить();
			НоваяСтрока.Имя = "РезервыТоваровОрганизаций";
			
			КорректировкаРегистров.Записать();
			
			Набор = РегистрыНакопления.РезервыТоваровОрганизаций.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(КорректировкаРегистров.Ссылка);
			Набор.Загрузить(ТаблицаНовыеРезервы);
			
			//Набор.ДополнительныеСвойства.Вставить(РегистрыНакопления.РезервыТоваровОрганизаций.ПризнакЗаписиНабораПриСвертке(), Истина);
			СтруктураВременныеТаблицы = Новый Структура("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
			Набор.ДополнительныеСвойства.Вставить("ДляПроведения", 
			Новый Структура("СтруктураВременныеТаблицы",СтруктураВременныеТаблицы));
			Набор.Записывать = Истина;
			Набор.Записать();
			
		КонецЕсли;
		
		
		// Если после распределения остались лишние резервы - их нужно просто сторнировать
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Период КАК Период,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.ВидЗапасов КАК ВидЗапасов,
		|	ВложенныйЗапрос.НомерГТД КАК НомерГТД,
		|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	СУММА(ВложенныйЗапрос.Резерв) - СУММА(ВложенныйЗапрос.КоличествоКПередаче) КАК ЛишнийРезерв
		|ПОМЕСТИТЬ ВтЛишниеРезервы
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТКоличествоКПередаче.Период КАК Период,
		|		ВТКоличествоКПередаче.Организация КАК Организация,
		|		ВТКоличествоКПередаче.ВидЗапасов КАК ВидЗапасов,
		|		ВТКоличествоКПередаче.НомерГТД КАК НомерГТД,
		|		ВТКоличествоКПередаче.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|		ВТКоличествоКПередаче.КоличествоКПередаче КАК КоличествоКПередаче,
		|		0 КАК Резерв
		|	ИЗ
		|		ВТКоличествоКПередаче КАК ВТКоличествоКПередаче
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		КОНЕЦПЕРИОДА(РезервыПоПериодам.Период, МЕСЯЦ),
		|		РезервыПоПериодам.Организация,
		|		РезервыПоПериодам.ВидЗапасов,
		|		РезервыПоПериодам.НомерГТД,
		|		РезервыПоПериодам.АналитикаУчетаНоменклатуры,
		|		0,
		|		РезервыПоПериодам.КоличествоОборот
		|	ИЗ
		|		РегистрНакопления.РезервыТоваровОрганизаций.ОстаткиИОбороты(&НачалоПериодаГраница, &КонецПериодаГраница, МЕСЯЦ, Движения, ) КАК РезервыПоПериодам
		|	ГДЕ
		|		РезервыПоПериодам.КоличествоОборот > 0) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Период,
		|	ВложенныйЗапрос.Организация,
		|	ВложенныйЗапрос.ВидЗапасов,
		|	ВложенныйЗапрос.НомерГТД,
		|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВложенныйЗапрос.Резерв) - СУММА(ВложенныйЗапрос.КоличествоКПередаче) > 0 И
		|	СУММА(ВложенныйЗапрос.Резерв) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаНоменклатуры,
		|	Организация,
		|	ВидЗапасов,
		|	Период,
		|	НомерГТД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОНЕЦПЕРИОДА(РезервыТоваровОрганизаций.Период, МЕСЯЦ) КАК Период,
		|	РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	РезервыТоваровОрганизаций.НомерГТД КАК НомерГТД,
		|	РезервыТоваровОрганизаций.Организация КАК Организация,
		|	РезервыТоваровОрганизаций.ВидЗапасов КАК ВидЗапасов,
		|	РезервыТоваровОрганизаций.КорОрганизация КАК КорОрганизация,
		|	РезервыТоваровОрганизаций.КорВидЗапасов КАК КорВидЗапасов,
		|	СУММА(ВЫБОР
		|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА РезервыТоваровОрганизаций.Количество
		|			ИНАЧЕ -РезервыТоваровОрганизаций.Количество
		|		КОНЕЦ) КАК Количество
		|ИЗ
		|	ВтЛишниеРезервы КАК ВтЛишниеРезервы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
		|		ПО (КОНЕЦПЕРИОДА(РезервыТоваровОрганизаций.Период, МЕСЯЦ) = ВтЛишниеРезервы.Период)
		|			И (РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры = ВтЛишниеРезервы.АналитикаУчетаНоменклатуры)
		|			И (РезервыТоваровОрганизаций.Организация = ВтЛишниеРезервы.Организация)
		|			И (РезервыТоваровОрганизаций.ВидЗапасов = ВтЛишниеРезервы.ВидЗапасов)
		|			И (РезервыТоваровОрганизаций.НомерГТД = ВтЛишниеРезервы.НомерГТД)
		|
		|СГРУППИРОВАТЬ ПО
		|	РезервыТоваровОрганизаций.Организация,
		|	РезервыТоваровОрганизаций.ВидЗапасов,
		|	РезервыТоваровОрганизаций.КорВидЗапасов,
		|	РезервыТоваровОрганизаций.КорОрганизация,
		|	КОНЕЦПЕРИОДА(РезервыТоваровОрганизаций.Период, МЕСЯЦ),
		|	РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры,
		|	РезервыТоваровОрганизаций.НомерГТД
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР
		|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА РезервыТоваровОрганизаций.Количество
		|			ИНАЧЕ -РезервыТоваровОрганизаций.Количество
		|		КОНЕЦ) > 0";
		
		Запрос.Текст = ТекстЗапроса;
		
		РезультатыЗапроса = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
		
		ЛишниеРезервы     = РезультатыЗапроса[0].Выгрузить();
		
		КолонкиПоиска = "АналитикаУчетаНоменклатуры,НомерГТД,Организация,ВидЗапасов";
		ОтборПоиска = Новый Структура(КолонкиПоиска);
		
		РезервыСКорЧастью = РезультатыЗапроса[1].Выгрузить();
		РезервыСКорЧастью.Индексы.Добавить(КолонкиПоиска);
		
		Если ЛишниеРезервы.Количество() > 0 Тогда
			
			КорректировкаРегистров = Документы.КорректировкаРегистров.СоздатьДокумент();
			КорректировкаРегистров.Дата = ТекущийМесяц;
			КорректировкаРегистров.Комментарий = НСтр("ru = 'Создан автоматически при сторнированнии лишнего резерва.'",
			ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			
			НоваяСтрока = КорректировкаРегистров.ТаблицаРегистров.Добавить();
			НоваяСтрока.Имя = "РезервыТоваровОрганизаций";
			
			КорректировкаРегистров.Записать();
			
			Набор = РегистрыНакопления.РезервыТоваровОрганизаций.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(КорректировкаРегистров.Ссылка);
			
			Для Каждого СтрТабл Из ЛишниеРезервы Цикл
				
				НераспределенныйЛишнийРезерв = СтрТабл.ЛишнийРезерв;
				ЗаполнитьЗначенияСвойств(ОтборПоиска, СтрТабл);
				
				КорЧасти = РезервыСКорЧастью.НайтиСтроки(ОтборПоиска);
				
				Для Каждого СтрМас из КорЧасти Цикл
					
					СторноКоличества = Мин(НераспределенныйЛишнийРезерв, СтрМас.Количество);
					
					СтрокаСторноПрихода = Набор.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаСторноПрихода, СтрМас);
					СтрокаСторноПрихода.ВидДвижения = ВидДвиженияНакопления.Приход;
					СтрокаСторноПрихода.Количество = - СторноКоличества;
					СтрокаСторноПрихода.ЭтоСторно  = Истина;
					
					СтрокаСторноРасхода = Набор.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаСторноРасхода, СтрокаСторноПрихода);
					СтрокаСторноРасхода.ВидДвижения    = ВидДвиженияНакопления.Расход;
					СтрокаСторноРасхода.Организация    = СтрокаСторноПрихода.КорОрганизация;
					СтрокаСторноРасхода.ВидЗапасов     = СтрокаСторноПрихода.КорВидЗапасов;
					СтрокаСторноРасхода.КорОрганизация = СтрокаСторноПрихода.Организация;
					СтрокаСторноРасхода.КорВидЗапасов  = СтрокаСторноПрихода.ВидЗапасов;
					
					НераспределенныйЛишнийРезерв = НераспределенныйЛишнийРезерв - СторноКоличества;
					
					Если НераспределенныйЛишнийРезерв = 0 Тогда
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
				Если НераспределенныйЛишнийРезерв <> 0 Тогда
					ТекстИсключения = НСтр("ru = 'Ошибка при распределении лишних резервов. Обратитесь к разработчикам.'");
					ВызватьИсключение ТекстИсключения;
				КонецЕсли;
				
			КонецЦикла;
			
			//Набор.ДополнительныеСвойства.Вставить(РегистрыНакопления.РезервыТоваровОрганизаций.ПризнакЗаписиНабораПриСвертке(), Истина);
			СтруктураВременныеТаблицы = Новый Структура("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
			Набор.ДополнительныеСвойства.Вставить("ДляПроведения", Новый Структура("СтруктураВременныеТаблицы",СтруктураВременныеТаблицы));
			Набор.Записывать = Истина;
			Набор.Записать();
			
			ЗафиксироватьТранзакцию();
			Возврат Истина;
			
		Иначе
			ЗафиксироватьТранзакцию();
			Возврат Истина;
		КонецЕсли;
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстОшибки = НСтр("ru = 'Возникла ошибка при переносе резервов товаров организаций за период %ТекущийМесяц%: %ПричинаОшибки%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПричинаОшибки%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ТекущийМесяц%", Формат(ТекущийМесяц, "ДЛФ=DD"));
		ЗаписьЖурналаРегистрации(ИмяСобытияПереносаРезервов(),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыНакопления.РезервыТоваровОрганизаций,
			,
			ТекстОшибки);
		
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

Функция ИмяСобытияПереносаРезервов() Экспорт
	Возврат НСтр("ru = 'Перенос резервов товаров организаций'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
КонецФункции

Процедура ЗаполнитьКОформлениюЗаПериод(Параметры, АдресВременногоХранилища) Экспорт
	
	Результат = РезультатФоновыхЗаданий();
	
	Если Параметры.Свойство("ОформлятьПередачи") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ОформлятьПередачи", Параметры.ОформлятьПередачи); 
		Запрос.УстановитьПараметр("ОформлятьВыкупы", Параметры.ОформлятьВыкупы); 
		
		НачалоПериода = НачалоМесяца(Параметры.Период.ДатаНачала);
		КонецПериода  = ?(ЗначениеЗаполнено(Параметры.Период.ДатаОкончания), КонецМесяца(Параметры.Период.ДатаОкончания), Параметры.Период.ДатаОкончания);
		
		Запрос.УстановитьПараметр("НачалоПериода",                 НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода",                  КонецПериода);
		Запрос.УстановитьПараметр("Отправитель",                   Параметры.Отправитель);
		Запрос.УстановитьПараметр("Получатель",                    Параметры.Получатель);
		Запрос.УстановитьПараметр("Склад",                         Параметры.Склад);
		
		Запрос.Текст = ЗапасыСервер.ТекстЗапросаОформленияПоРезервамТоваровОрганизаций(Запрос, "ВыборкаПоШапке");
		
		КОформлениюЗаПериод = Запрос.Выполнить().Выгрузить();
		Результат.КОформлениюЗаПериод                      = КОформлениюЗаПериод;
	КонецЕсли;
	
	Результат.ЛишниеРезервы                            = ЕстьЛишниеРезервы();
	Результат.ЕстьТоварыКОформлениюПередачПередВыкупом = ЕстьТоварыКОформлениюПередачПередВыкупом(Параметры);
	
	ПоместитьВоВременноеХранилище(Результат, АдресВременногоХранилища); 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
