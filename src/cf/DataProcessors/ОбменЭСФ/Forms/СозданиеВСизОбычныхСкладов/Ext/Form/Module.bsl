
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ВыбранныеОрганизации") Тогда
		ЗаполнитьДеревоОрганизацииИСклады(Параметры.ВыбранныеОрганизации);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоОрганизацииИСклады(ВыбранныеОрганизации)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЛОЖЬ КАК СоздатьВС,
	|	Склады.Ссылка КАК Склад,
	|	Организации.Ссылка КАК Организация
	|ПОМЕСТИТЬ СкладыПоОрганизациям
	|ИЗ
	|	Справочник.Склады КАК Склады,
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В(&ВыбранныеОрганизации)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкладыПоОрганизациям.СоздатьВС,
	|	СкладыПоОрганизациям.Склад,
	|	СкладыПоОрганизациям.Организация КАК Организация,
	|	СоответствиеСкладовИВиртуальныхСкладов.ВиртуальныйСклад
	|ИЗ
	|	СкладыПоОрганизациям КАК СкладыПоОрганизациям
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеСкладовИВиртуальныхСкладов КАК СоответствиеСкладовИВиртуальныхСкладов
	|		ПО СкладыПоОрганизациям.Склад = СоответствиеСкладовИВиртуальныхСкладов.Склад
	|			И СкладыПоОрганизациям.Организация = СоответствиеСкладовИВиртуальныхСкладов.Организация
	|ИТОГИ ПО
	|	Организация";
	
	Запрос.УстановитьПараметр("ВыбранныеОрганизации", ВыбранныеОрганизации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЗначениеВРеквизитФормы(ВыборкаДетальныеЗаписи, "ВыбранныеСклады");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьИСохранитьВиртуальныеСклады()
	
	МассивСоответствийВС = Новый Массив;
	МассивСкладовСНаименованиями = Новый Массив;
	ПроверитьСоответствияИНаименования(МассивСоответствийВС, МассивСкладовСНаименованиями);
	
	Если МассивСоответствийВС.Количество() > 0 ИЛИ МассивСкладовСНаименованиями.Количество() > 0 Тогда
		
		ТекстСообщения = "";
		
		ТекстСообщенияСоответствий = "";		
		ТекстСообщенияНаименований = "";
		
		Если МассивСоответствийВС.Количество() > 0 Тогда
			
			ТекстСообщенияСоответствий = НСтр("ru='Склады %1 уже имеют соответствия виртуальных складов. Для них установленные соответствия останутся неизменными'") + Символы.ПС + Символы.ПС;
			
			ТекстСообщенияСоответствий = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияСоответствий,
			СтрСоединить(МассивСоответствийВС, ", "));
			ТекстСообщения = ТекстСообщения + ТекстСообщенияСоответствий;
			
		КонецЕсли;
		
		Если МассивСкладовСНаименованиями.Количество() > 0 Тогда
			
			ТекстСообщенияНаименований = НСтр("ru='В системе уже присутствуют виртуальные склады с наименованиями %1. Продолжив, вы создадите склады с такими же наименованиями.'") + Символы.ПС + Символы.ПС;
			
			ТекстСообщенияНаименований = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщенияНаименований,
			СтрСоединить(МассивСоответствийВС, ", "));
			ТекстСообщения = ТекстСообщения + ТекстСообщенияНаименований;
			
		КонецЕсли;
		
		ТекстСообщения = ТекстСообщения + НСтр("ru='Хотите продолжить?'");
		
		ПоказатьВопрос(Новый ОписаниеОповещения("СоздатьИСохранитьВиртуальныеСкладыПослеВопроса", ЭтотОбъект),
		ТекстСообщения, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		СоздатьИСохранитьВиртуальныеСкладыПослеВопроса(КодВозвратаДиалога.Да, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьИСохранитьВиртуальныеСкладыПослеВопроса(РезультатВопроса, ПараметрыЗаписи) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		СоздатьИСохранитьВыбранныеСкладыНаСервере();
		
		ЗначениеОтбора = Новый Структура("Статус", ПредопределенноеЗначение("Перечисление.СтатусыВиртуальныхСкладов.НеСозданВС"));
		
		ПараметрыВыбора = Новый Структура("Отбор", ЗначениеОтбора);	
		ОткрытьФорму("Справочник.ВиртуальныеСклады.ФормаСписка",ПараметрыВыбора,,,);

		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьИСохранитьВыбранныеСкладыНаСервере()
	
	СправочникВС = Справочники.ВиртуальныеСклады;
	ДеревоВыбранныеСклады = РеквизитФормыВЗначение("ВыбранныеСклады");
	
	Для Каждого ГруппаОрганизация Из ДеревоВыбранныеСклады.Строки Цикл
		
		Для Каждого Строка из ГруппаОрганизация.Строки Цикл
			Если Строка <> Неопределено И Строка.СоздатьВС Тогда
				ЭлементСправочника 						= СправочникВС.СоздатьЭлемент();
				ЭлементСправочника.Организация 			= ГруппаОрганизация.Организация;                                                
				ЭлементСправочника.Наименование 		= Строка.Склад.Наименование;
				ЭлементСправочника.Статус		        = Перечисления.СтатусыВиртуальныхСкладов.НеСозданВС;
				Попытка
					
					ЭлементСправочника.Записать();
					//Запись соответствия обычного склада и виртуального
					Если ЗначениеЗаполнено(Строка.Склад) 
						И ЗначениеЗаполнено(ЭлементСправочника.Ссылка)
						И ЗначениеЗаполнено(ГруппаОрганизация.Организация)
						И НЕ ЗначениеЗаполнено(Строка.ВиртуальныйСклад) Тогда
						МенеджерЗаписи = РегистрыСведений.СоответствиеСкладовИВиртуальныхСкладов.СоздатьМенеджерЗаписи();						
						
						МенеджерЗаписи.Организация = ГруппаОрганизация.Организация; 
						МенеджерЗаписи.Склад = Строка.Склад; 
						МенеджерЗаписи.ВиртуальныйСклад = ЭлементСправочника.Ссылка;
						МенеджерЗаписи.Записать();
					КонецЕсли;	

				Исключение
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ПроверитьСоответствияИНаименования(МассивСоответствийВС, МассивСкладовСНаименованиями)
	
	СправочникВС = Справочники.ВиртуальныеСклады;
	ДеревоВыбранныеСклады = РеквизитФормыВЗначение("ВыбранныеСклады");
	
	Для Каждого ГруппаОрганизация Из ДеревоВыбранныеСклады.Строки Цикл
		
		Для Каждого Строка из ГруппаОрганизация.Строки Цикл
			
			Если Строка <> Неопределено И Строка.СоздатьВС Тогда
				
				Если ЗначениеЗаполнено(Строка.ВиртуальныйСклад)  Тогда
					МассивСоответствийВС.Добавить(Строка.Склад);		
				КонецЕсли;
				
				НайденныйВСПоНаименованию = Справочники.ВиртуальныеСклады.НайтиПоНаименованию(Строка.Склад.Наименование, Истина);
				
				Если ЗначениеЗаполнено(НайденныйВСПоНаименованию) Тогда
					МассивСкладовСНаименованиями.Добавить(НайденныйВСПоНаименованию.Наименование);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	СоздатьИСохранитьВиртуальныеСклады();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеСкладыСоздатьВСПриИзменении(Элемент)
	Если ПустаяСтрока(Элементы.ВыбранныеСклады.ТекущиеДанные.Склад) Тогда
		СтрокиДерева = ВыбранныеСклады.ПолучитьЭлементы();
		ВыбраннаяОрганизация = Элементы.ВыбранныеСклады.ТекущиеДанные.Организация; 
		Для каждого СтрокаОрганизация Из СтрокиДерева Цикл
			Если СтрокаОрганизация.Организация = ВыбраннаяОрганизация Тогда
				СтрокиПоОрганизации = СтрокаОрганизация.ПолучитьЭлементы();
				Для	Каждого СтрокаСклад из СтрокиПоОрганизации Цикл
					СтрокаСклад.СоздатьВС = СтрокаОрганизация.СоздатьВС;
				КонецЦикла;	
			КонецЕсли;
		КонецЦикла;		
	КонецЕсли;
КонецПроцедуры
