//кт Начало Апаев_О [28.08.2022]
Процедура ОбновитьСоставПакета(Пакет, НачалоПериода = Неопределено, КонецПериода = Неопределено) Экспорт
	
	МакетОтчетПоВыручке = ПолучитьМакет("ОтчетПоВыручке");
	
	Если НачалоПериода = Неопределено Тогда
		НачалоПериода = НачалоМесяца(ТекущаяДата());
	КонецЕсли;
	
	Если КонецПериода = Неопределено Тогда
		КонецПериода = КонецДня(ТекущаяДата());
	КонецЕсли;
	
	//Получение дополнительных данных	
	Результат = ПолучитьДополнительныеДанные();
	
	ВыборкаПоказатели = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Год");
	
	ШапкаПериоды     = МакетОтчетПоВыручке.ПолучитьОбласть("ШапкаПериоды");
	ДанныеПлан       = МакетОтчетПоВыручке.ПолучитьОбласть("ДанныеПлан");	
	ОбластьДиаграмма = МакетОтчетПоВыручке.ПолучитьОбласть("Диаграмма");
	
	Диаграмма = ОбластьДиаграмма.Рисунки.D1.Объект;	
	Диаграмма.Серии.Очистить();
	
	МассивПоказателей = Новый Массив;
	МассивПоказателей.Добавить(ШапкаПериоды);
		
	Для Каждого Элемент Из ПолучитьСоответствиеМесяцев() Цикл
		Диаграмма.Точки.Добавить(НРег(Элемент.Значение));
	КонецЦикла;
	
	//Вывод показателей
	Пока ВыборкаПоказатели.Следующий() Цикл
		
		Если НЕ ВыборкаПоказатели.Год = "План" Тогда
			ТекущаяСерия = Диаграмма.Серии.Добавить(СтрЗаменить(ВыборкаПоказатели.Год, " ", ""));
			ТекущаяСерия.Маркер = ТипМаркераДиаграммы.Круг;
		КонецЕсли;
		
		ДанныеПериодов = МакетОтчетПоВыручке.ПолучитьОбласть("ДанныеПериодов");
		ДанныеПериодов.Параметры.Год = СтрЗаменить(ВыборкаПоказатели.Год, " ", "");
		
		ВыборкаПериоды = ВыборкаПоказатели.Выбрать();
		
		Пока ВыборкаПериоды.Следующий() Цикл
			
			Значение = ?(ВыборкаПериоды.Значение = 0, "", ВыборкаПериоды.Значение); 
			
			ДанныеПериодов.Параметры["Сумма" + ВыборкаПериоды.НомерМесяца] = Значение;
			
			Если НЕ ВыборкаПоказатели.Год = "План" Тогда			
				Точка = Диаграмма.Точки.Получить(ВыборкаПериоды.НомерМесяца - 1);
				Диаграмма.УстановитьЗначение(Точка, ТекущаяСерия, Значение);
			КонецЕсли;
			
		КонецЦикла;
		
		ДанныеПериодов.Параметры.СуммаИтог = ВыборкаПоказатели.Значение;
		
		МассивПоказателей.Добавить(ДанныеПериодов);
		
	КонецЦикла;
	
	//Получение основных данных
	Результат = ПолучитьДанныеОтчета(НачалоПериода, КонецПериода);
	
	ВыборкаДанные = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период");
	
	Пока ВыборкаДанные.Следующий() Цикл
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		//Обработка табличного документа
		ОбработатьТабличныйДокумент(ВыборкаДанные, ТабличныйДокумент, МакетОтчетПоВыручке);
		
		//Вывод диаграммы
		ТабличныйДокумент.Вывести(ОбластьДиаграмма);
		
		//Вывод показателей
		Для Каждого Показатель Из МассивПоказателей Цикл
			ТабличныйДокумент.Присоединить(Показатель);
		КонецЦикла;
		
		//Добавление табличного документа в пакет
		ЭлементПакета 			   = Пакет.Состав.Добавить();
		ЭлементПакета.Наименование = Строка(Формат(ВыборкаДанные.Период, "ДФ=dd.MM.yyyy"));
		ЭлементПакета.Данные 	   = ПоместитьВоВременноеХранилище(ТабличныйДокумент, Новый УникальныйИдентификатор);
		
	КонецЦикла;
	
КонецПроцедуры

//Вывод основных данных
Процедура ОбработатьТабличныйДокумент(ВыборкаДанные, ТабличныйДокумент, МакетОтчетПоВыручке)
	
	Шапка  	= МакетОтчетПоВыручке.ПолучитьОбласть("Шапка");
	Строка 	= МакетОтчетПоВыручке.ПолучитьОбласть("Строка");
	Итоги  	= МакетОтчетПоВыручке.ПолучитьОбласть("Итоги");
	Пустота = МакетОтчетПоВыручке.ПолучитьОбласть("Пустота");
	
	ВыборкаВидыОплаты = ВыборкаДанные.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ВидОплаты");
	
	Пока ВыборкаВидыОплаты.Следующий() Цикл
		
		//Шапка
		Шапка.Параметры.Период = Формат(ВыборкаВидыОплаты.Период, "ДФ=dd.MM.yyyy") + "-" + НРег(Формат(ВыборкаВидыОплаты.Период, "ДФ=дддд"));	
		ТабличныйДокумент.Вывести(Шапка);
		
		//Строки
		НомерСтроки = 1;
		
		Выборка = ВыборкаВидыОплаты.Выбрать();
		
		Пока Выборка.Следующий() Цикл			
			ЗаполнитьЗначенияСвойств(Строка.Параметры, Выборка);			
			Строка.Параметры.НомерСтроки = НомерСтроки;
			ТабличныйДокумент.Вывести(Строка);		
			НомерСтроки = НомерСтроки + 1;			
		КонецЦикла;
		
		//Итоги 
		Если ВыборкаВидыОплаты.ВидОплаты = Перечисления.кт_ВидыПоступленияОплаты.Магазин Тогда		
			Итоги.Параметры.ТекстИтог = "Итого по магазинам";	
		Иначе
			Итоги.Параметры.ТекстИтог = "Итого дилеры";
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Итоги.Параметры, ВыборкаВидыОплаты);
		ТабличныйДокумент.Вывести(Итоги);
		
		ТабличныйДокумент.Вывести(Пустота);
		ТабличныйДокумент.Вывести(Пустота);
		
	КонецЦикла;
	
КонецПроцедуры

//Месяца
Функция ПолучитьСоответствиеМесяцев()
	
	Месяца = Новый Соответствие;
	Месяца.Вставить(1,  "Январь");
	Месяца.Вставить(2,  "Февраль");
	Месяца.Вставить(3,  "Март");
	Месяца.Вставить(4,  "Апрель");
	Месяца.Вставить(5,  "Май");
	Месяца.Вставить(6,  "Июнь");
	Месяца.Вставить(7,  "Июль");
	Месяца.Вставить(8,  "Август");
	Месяца.Вставить(9,  "Сентябрь");
	Месяца.Вставить(10, "Октябрь");
	Месяца.Вставить(11, "Ноябрь");
	Месяца.Вставить(12, "Декабрь");
	
	Возврат Месяца;
	
КонецФункции

//Результат данных отчета по выручке за период
Функция ПолучитьДанныеОтчета(НачалоПериода, КонецПериода)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("НачалоПериода",	НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", 	КонецПериода);
			
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Календарь.Дата КАК Период
	|ПОМЕСТИТЬ ВТ_Периоды
	|ИЗ
	|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК Календарь
	|ГДЕ
	|	Календарь.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ)
	|
	|СГРУППИРОВАТЬ ПО
	|	Календарь.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ПриходныйКассовыйОрдер.Дата, ДЕНЬ) КАК Период,
	|	Кассы.Подразделение КАК Подразделение,
	|	ПриходныйКассовыйОрдер.Контрагент КАК Контрагент,
	|	ПриходныйКассовыйОрдер.кт_ВидПоступленияОплаты КАК ВидОплаты,
	|	СУММА(ЕСТЬNULL(ПриходныйКассовыйОрдер.СуммаДокумента, 0)) КАК Сумма,
	|	""Наличка"" КАК ТипСредств
	|ПОМЕСТИТЬ ВТ_ДанныеИсточников
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Кассы КАК Кассы
	|		ПО ПриходныйКассовыйОрдер.Касса = Кассы.Ссылка
	|ГДЕ
	|	ПриходныйКассовыйОрдер.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ)
	|	И ПриходныйКассовыйОрдер.Проведен = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ПриходныйКассовыйОрдер.Дата, ДЕНЬ),
	|	Кассы.Подразделение,
	|	ПриходныйКассовыйОрдер.Контрагент,
	|	ПриходныйКассовыйОрдер.кт_ВидПоступленияОплаты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ОперацияПоПлатежнойКарте.Дата, ДЕНЬ),
	|	ОперацияПоПлатежнойКарте.Подразделение,
	|	ОперацияПоПлатежнойКарте.Контрагент,
	|	ОперацияПоПлатежнойКарте.кт_ВидПоступленияОплаты,
	|	СУММА(ЕСТЬNULL(ОперацияПоПлатежнойКарте.СуммаДокумента, 0)),
	|	""Карта""
	|ИЗ
	|	Документ.ОперацияПоПлатежнойКарте КАК ОперацияПоПлатежнойКарте
	|ГДЕ
	|	ОперацияПоПлатежнойКарте.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ)
	|	И ОперацияПоПлатежнойКарте.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
	|	И ОперацияПоПлатежнойКарте.Проведен = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ОперацияПоПлатежнойКарте.Дата, ДЕНЬ),
	|	ОперацияПоПлатежнойКарте.Подразделение,
	|	ОперацияПоПлатежнойКарте.Контрагент,
	|	ОперацияПоПлатежнойКарте.кт_ВидПоступленияОплаты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(БДС.Дата, ДЕНЬ),
	|	Платежи.ОснованиеПлатежа.Подразделение,
	|	Платежи.ОснованиеПлатежа.Контрагент,
	|	БДС.кт_ВидПоступленияОплаты,
	|	СУММА(ЕСТЬNULL(Платежи.СуммаВзаиморасчетов, 0)),
	|	""Перечисление""
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК БДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК Платежи
	|		ПО БДС.Ссылка = Платежи.Ссылка
	|ГДЕ
	|	БДС.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ)
	|	И БДС.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту))
	|	И БДС.Проведен = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(БДС.Дата, ДЕНЬ),
	|	Платежи.ОснованиеПлатежа.Подразделение,
	|	Платежи.ОснованиеПлатежа.Контрагент,
	|	БДС.кт_ВидПоступленияОплаты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(РасходныйКассовыйОрдер.Дата, ДЕНЬ),
	|	РасходныйКассовыйОрдер.Подразделение,
	|	РасходныйКассовыйОрдер.Контрагент,
	|	РасходныйКассовыйОрдер.кт_ВидПоступленияОплаты,
	|	СУММА(ЕСТЬNULL(РасходныйКассовыйОрдер.СуммаДокумента, 0)),
	|	""Инкасация""
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|ГДЕ
	|	РасходныйКассовыйОрдер.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ)
	|	И РасходныйКассовыйОрдер.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИнкассацияДенежныхСредствВБанк)
	|	И РасходныйКассовыйОрдер.Проведен = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(РасходныйКассовыйОрдер.Дата, ДЕНЬ),
	|	РасходныйКассовыйОрдер.Подразделение,
	|	РасходныйКассовыйОрдер.Контрагент,
	|	РасходныйКассовыйОрдер.кт_ВидПоступленияОплаты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(РасходныйКассовыйОрдер.Дата, ДЕНЬ),
	|	РасходныйКассовыйОрдер.Подразделение,
	|	РасходныйКассовыйОрдер.Контрагент,
	|	РасходныйКассовыйОрдер.кт_ВидПоступленияОплаты,
	|	СУММА(ЕСТЬNULL(РасходныйКассовыйОрдер.СуммаДокумента, 0)),
	|	""ВозвратНаличные""
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|ГДЕ
	|	РасходныйКассовыйОрдер.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ)
	|	И РасходныйКассовыйОрдер.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	|	И РасходныйКассовыйОрдер.Проведен = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(РасходныйКассовыйОрдер.Дата, ДЕНЬ),
	|	РасходныйКассовыйОрдер.Подразделение,
	|	РасходныйКассовыйОрдер.Контрагент,
	|	РасходныйКассовыйОрдер.кт_ВидПоступленияОплаты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ОперацияПоПлатежнойКарте.Дата, ДЕНЬ),
	|	ОперацияПоПлатежнойКарте.Подразделение,
	|	ОперацияПоПлатежнойКарте.Контрагент,
	|	ОперацияПоПлатежнойКарте.кт_ВидПоступленияОплаты,
	|	СУММА(ЕСТЬNULL(ОперацияПоПлатежнойКарте.СуммаДокумента, 0)),
	|	""ВозвратКарта""
	|ИЗ
	|	Документ.ОперацияПоПлатежнойКарте КАК ОперацияПоПлатежнойКарте
	|ГДЕ
	|	ОперацияПоПлатежнойКарте.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ)
	|	И ОперацияПоПлатежнойКарте.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	|	И ОперацияПоПлатежнойКарте.Проведен = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ОперацияПоПлатежнойКарте.Дата, ДЕНЬ),
	|	ОперацияПоПлатежнойКарте.Подразделение,
	|	ОперацияПоПлатежнойКарте.Контрагент,
	|	ОперацияПоПлатежнойКарте.кт_ВидПоступленияОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеИсточников.Период КАК Период,
	|	ВТ_ДанныеИсточников.Подразделение КАК Подразделение,
	|	ВТ_ДанныеИсточников.Контрагент КАК Контрагент,
	|	ВТ_ДанныеИсточников.ВидОплаты КАК ВидОплаты,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ДанныеИсточников.ТипСредств = ""Наличка""
	|				ТОГДА ВТ_ДанныеИсточников.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Наличка,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ДанныеИсточников.ТипСредств = ""Карта""
	|				ТОГДА ВТ_ДанныеИсточников.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Карта,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ДанныеИсточников.ТипСредств = ""ВозвратНаличные""
	|				ТОГДА ВТ_ДанныеИсточников.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВозвратНаличка,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ДанныеИсточников.ТипСредств = ""ВозвратКарта""
	|				ТОГДА ВТ_ДанныеИсточников.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВозвратКарта,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ДанныеИсточников.ТипСредств = ""Инкасация""
	|				ТОГДА ВТ_ДанныеИсточников.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Инкасация,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ДанныеИсточников.ТипСредств = ""Перечисление""
	|				ТОГДА ВТ_ДанныеИсточников.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Перечисление
	|ПОМЕСТИТЬ ВТ_ДанныеПоТипамСредств
	|ИЗ
	|	ВТ_ДанныеИсточников КАК ВТ_ДанныеИсточников
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеИсточников.Период,
	|	ВТ_ДанныеИсточников.Подразделение,
	|	ВТ_ДанныеИсточников.Контрагент,
	|	ВТ_ДанныеИсточников.ВидОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеПоТипамСредств.Период КАК Период,
	|	ВТ_ДанныеПоТипамСредств.Подразделение КАК Подразделение,
	|	ВТ_ДанныеПоТипамСредств.Контрагент КАК Контрагент,
	|	ВТ_ДанныеПоТипамСредств.ВидОплаты КАК ВидОплаты,
	|	СУММА(ВТ_ДанныеПоТипамСредств.Наличка) КАК Наличка,
	|	СУММА(ВТ_ДанныеПоТипамСредств.Карта) КАК Карта,
	|	СУММА(ВТ_ДанныеПоТипамСредств.ВозвратНаличка) КАК ВозвратНаличка,
	|	СУММА(ВТ_ДанныеПоТипамСредств.ВозвратКарта) КАК ВозвратКарта,
	|	СУММА(ВТ_ДанныеПоТипамСредств.Инкасация) КАК Инкасация,
	|	СУММА(ВТ_ДанныеПоТипамСредств.Перечисление) КАК Перечисление,
	|	СУММА(ЕСТЬNULL(ВТ_ДанныеПоТипамСредств.Наличка, 0) + ЕСТЬNULL(ВТ_ДанныеПоТипамСредств.Карта, 0) - ЕСТЬNULL(ВТ_ДанныеПоТипамСредств.ВозвратНаличка, 0) - ЕСТЬNULL(ВТ_ДанныеПоТипамСредств.ВозвратКарта, 0) + ЕСТЬNULL(ВТ_ДанныеПоТипамСредств.Перечисление, 0)) КАК ИтогоЗаДень
	|ПОМЕСТИТЬ ВТ_ОбщиеДанные
	|ИЗ
	|	ВТ_ДанныеПоТипамСредств КАК ВТ_ДанныеПоТипамСредств
	|ГДЕ
	|	НЕ ВТ_ДанныеПоТипамСредств.Подразделение ЕСТЬ NULL
	|	И ВТ_ДанныеПоТипамСредств.ВидОплаты В (ЗНАЧЕНИЕ(Перечисление.кт_ВидыПоступленияОплаты.Магазин), ЗНАЧЕНИЕ(Перечисление.кт_ВидыПоступленияОплаты.Дилер))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеПоТипамСредств.Период,
	|	ВТ_ДанныеПоТипамСредств.Подразделение,
	|	ВТ_ДанныеПоТипамСредств.Контрагент,
	|	ВТ_ДанныеПоТипамСредств.ВидОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Периоды.Период КАК Период,
	|	ВТ_ОбщиеДанные.Подразделение КАК Подразделение,
	|	ВТ_ОбщиеДанные.Контрагент КАК Контрагент,
	|	ВТ_ОбщиеДанные.ВидОплаты КАК ВидОплаты
	|ПОМЕСТИТЬ ВТ_Владельцы
	|ИЗ
	|	ВТ_Периоды КАК ВТ_Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбщиеДанные КАК ВТ_ОбщиеДанные
	|		ПО (ИСТИНА)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОбщиеДанные.Подразделение,
	|	ВТ_ОбщиеДанные.Контрагент,
	|	ВТ_ОбщиеДанные.ВидОплаты,
	|	ВТ_Периоды.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Владельцы.Период КАК Период,
	|	ВТ_Владельцы.Подразделение КАК Подразделение,
	|	ВТ_Владельцы.Контрагент КАК Контрагент,
	|	ВТ_Владельцы.ВидОплаты КАК ВидОплаты,
	|	СУММА(ЕСТЬNULL(ВТ_ОбщиеДанные.ИтогоЗаДень, 0)) КАК ИтогоЗаДень
	|ПОМЕСТИТЬ ВТ_ИтогоПоДням
	|ИЗ
	|	ВТ_Владельцы КАК ВТ_Владельцы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбщиеДанные КАК ВТ_ОбщиеДанные
	|		ПО ВТ_Владельцы.Период > ВТ_ОбщиеДанные.Период
	|			И ВТ_Владельцы.Подразделение = ВТ_ОбщиеДанные.Подразделение
	|			И ВТ_Владельцы.Контрагент = ВТ_ОбщиеДанные.Контрагент
	|			И ВТ_Владельцы.ВидОплаты = ВТ_ОбщиеДанные.ВидОплаты
	|ГДЕ
	|	НЕ ЕСТЬNULL(ВТ_ОбщиеДанные.ИтогоЗаДень, 0) = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Владельцы.Период,
	|	ВТ_Владельцы.Подразделение,
	|	ВТ_Владельцы.Контрагент,
	|	ВТ_Владельцы.ВидОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Владельцы.Период КАК Период,
	|	ВТ_Владельцы.Подразделение КАК Подразделение,
	|	ВТ_Владельцы.Контрагент КАК Контрагент,
	|	ВТ_Владельцы.ВидОплаты КАК ВидОплаты,
	|	СУММА(ЕСТЬNULL(ВТ_ОбщиеДанные.Наличка, 0)) КАК Наличка,
	|	СУММА(ЕСТЬNULL(ВТ_ОбщиеДанные.Карта, 0)) КАК Карта,
	|	СУММА(ЕСТЬNULL(ВТ_ОбщиеДанные.ВозвратНаличка, 0)) КАК ВозвратНаличка,
	|	СУММА(ЕСТЬNULL(ВТ_ОбщиеДанные.ВозвратКарта, 0)) КАК ВозвратКарта,
	|	СУММА(ЕСТЬNULL(ВТ_ОбщиеДанные.Инкасация, 0)) КАК Инкасация,
	|	СУММА(ЕСТЬNULL(ВТ_ОбщиеДанные.Перечисление, 0)) КАК Перечисление,
	|	СУММА(ЕСТЬNULL(ВТ_ОбщиеДанные.ИтогоЗаДень, 0)) КАК ИтогоЗаДень
	|ПОМЕСТИТЬ ВТ_Финал
	|ИЗ
	|	ВТ_Владельцы КАК ВТ_Владельцы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбщиеДанные КАК ВТ_ОбщиеДанные
	|		ПО ВТ_Владельцы.Период = ВТ_ОбщиеДанные.Период
	|			И ВТ_Владельцы.Подразделение = ВТ_ОбщиеДанные.Подразделение
	|			И ВТ_Владельцы.Контрагент = ВТ_ОбщиеДанные.Контрагент
	|			И ВТ_Владельцы.ВидОплаты = ВТ_ОбщиеДанные.ВидОплаты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Владельцы.Период,
	|	ВТ_Владельцы.Подразделение,
	|	ВТ_Владельцы.Контрагент,
	|	ВТ_Владельцы.ВидОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Финал.Период КАК Период,
	|	ВТ_Финал.Подразделение КАК Подразделение,
	|	ВТ_Финал.Контрагент КАК Контрагент,
	|	ВТ_Финал.ВидОплаты КАК ВидОплаты,
	|	СУММА(ВТ_Финал.Наличка) КАК Наличка,
	|	СУММА(ВТ_Финал.Карта) КАК Карта,
	|	СУММА(ВТ_Финал.ВозвратНаличка) КАК ВозвратНаличка,
	|	СУММА(ВТ_Финал.ВозвратКарта) КАК ВозвратКарта,
	|	СУММА(ВТ_Финал.Инкасация) КАК Инкасация,
	|	СУММА(ВТ_Финал.Перечисление) КАК Перечисление,
	|	СУММА(ВТ_Финал.ИтогоЗаДень) КАК ИтогоЗаДень,
	|	СУММА(ВТ_ИтогоПоДням.ИтогоЗаДень) КАК ИтогоЗаПериод
	|ПОМЕСТИТЬ ВТ_Итог
	|ИЗ
	|	ВТ_Финал КАК ВТ_Финал
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИтогоПоДням КАК ВТ_ИтогоПоДням
	|		ПО ВТ_Финал.Период = ВТ_ИтогоПоДням.Период
	|			И ВТ_Финал.Подразделение = ВТ_ИтогоПоДням.Подразделение
	|			И ВТ_Финал.Контрагент = ВТ_ИтогоПоДням.Контрагент
	|			И ВТ_Финал.ВидОплаты = ВТ_ИтогоПоДням.ВидОплаты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Финал.Период,
	|	ВТ_Финал.Подразделение,
	|	ВТ_Финал.Контрагент,
	|	ВТ_Финал.ВидОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Итог.Период КАК Период,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_Итог.Подразделение.кт_НаименованиеДляОтчетов, """") = """"
	|			ТОГДА ВТ_Итог.Подразделение
	|		ИНАЧЕ ВТ_Итог.Подразделение.кт_НаименованиеДляОтчетов
	|	КОНЕЦ КАК Подразделение,
	|	СУММА(ВЫРАЗИТЬ(ВТ_Итог.Наличка КАК ЧИСЛО(15, 0))) КАК Наличка,
	|	СУММА(ВЫРАЗИТЬ(ВТ_Итог.Карта КАК ЧИСЛО(15, 0))) КАК Карта,
	|	СУММА(ВЫРАЗИТЬ(ВТ_Итог.ВозвратНаличка КАК ЧИСЛО(15, 0))) КАК ВозвратНаличка,
	|	СУММА(ВЫРАЗИТЬ(ВТ_Итог.ВозвратКарта КАК ЧИСЛО(15, 0))) КАК ВозвратКарта,
	|	СУММА(ВЫРАЗИТЬ(ВТ_Итог.Инкасация КАК ЧИСЛО(15, 0))) КАК Инкасация,
	|	СУММА(ВЫРАЗИТЬ(ВТ_Итог.Перечисление КАК ЧИСЛО(15, 0))) КАК Перечисление,
	|	СУММА(ВЫРАЗИТЬ(ВТ_Итог.ИтогоЗаДень КАК ЧИСЛО(15, 0))) КАК ИтогоЗаДень,
	|	СУММА(ВЫРАЗИТЬ(ЕСТЬNULL(ВТ_Итог.ИтогоЗаПериод, 0) + ЕСТЬNULL(ВТ_Итог.ИтогоЗаДень, 0) КАК ЧИСЛО(15, 0))) КАК ИтогоЗаПериод,
	|	ВТ_Итог.ВидОплаты КАК ВидОплаты
	|ИЗ
	|	ВТ_Итог КАК ВТ_Итог
	|ГДЕ
	|	ВТ_Итог.Период МЕЖДУ &НачалоПериода И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Итог.Период,
	|	ВТ_Итог.ВидОплаты,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_Итог.Подразделение.кт_НаименованиеДляОтчетов, """") = """"
	|			ТОГДА ВТ_Итог.Подразделение
	|		ИНАЧЕ ВТ_Итог.Подразделение.кт_НаименованиеДляОтчетов
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	ВидОплаты УБЫВ,
	|	ИтогоЗаПериод УБЫВ
	|ИТОГИ
	|	СУММА(Наличка),
	|	СУММА(Карта),
	|	СУММА(ВозвратНаличка),
	|	СУММА(ВозвратКарта),
	|	СУММА(Инкасация),
	|	СУММА(Перечисление),
	|	СУММА(ИтогоЗаДень),
	|	СУММА(ИтогоЗаПериод)
	|ПО
	|	Период,
	|	ВидОплаты";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

//Структуру дополнительных данных
Функция ПолучитьДополнительныеДанные()
		
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТекущийГод", Год(ТекущаяДата()));
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Показатели.ПлановыеПоказатели
	|			ТОГДА ""План""
	|		ИНАЧЕ ГОД(ЗначенияПоказателей.Ссылка.Год)
	|	КОНЕЦ КАК Год,
	|	ЗначенияПоказателей.Период КАК Период,
	|	ЗначенияПоказателей.Значение КАК Значение,
	|	МЕСЯЦ(ЗначенияПоказателей.Период) КАК НомерМесяца
	|ИЗ
	|	Справочник.кт_ЗначенияПоказателейВыручки КАК Показатели
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.кт_ЗначенияПоказателейВыручки.ЗначениеПоказателей КАК ЗначенияПоказателей
	|		ПО Показатели.Ссылка = ЗначенияПоказателей.Ссылка
	|			И (ВЫБОР
	|				КОГДА Показатели.ПлановыеПоказатели
	|					ТОГДА ГОД(Показатели.Год) = &ТекущийГод
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗначенияПоказателей.Период,
	|	ЗначенияПоказателей.Значение,
	|	ВЫБОР
	|		КОГДА Показатели.ПлановыеПоказатели
	|			ТОГДА ""План""
	|		ИНАЧЕ ГОД(ЗначенияПоказателей.Ссылка.Год)
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Год,
	|	Период,
	|	НомерМесяца
	|ИТОГИ
	|	СУММА(Значение)
	|ПО
	|	Год";
	
	Возврат Запрос.Выполнить();
	
КонецФункции
//кт Окончание Апаев_О [28.08.2022]