////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЭСФСерверПереопределяемый.ПроверкаПоддержкиМеханизмаВАвтономномРабочемМесте() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	СтруктурнаяЕдиница = ЭСФСерверПереопределяемый.ПолучитьОрганизациюпоУмолчанию();
	
	Если НЕ ЭСФКлиентСерверПереопределяемый.ИспользуютсяСтруктурныеПодразделения() Тогда
		Элементы.СтруктурнаяЕдиница.Заголовок = НСтр("ru = 'Организация'");
		Элементы.СтруктурнаяЕдиница.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Организации");
		Элементы.ЗагружаемыеСНТСтруктурноеПодразделение.Видимость = Ложь;
	КонецЕсли;
	
	СтараяСтруктурнаяЕдиница = СтруктурнаяЕдиница;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Контейнер = СНТКлиентСервер.КонтейнерМетодов();	
	Контейнер.ПриОткрытииФормы(ЭтаФорма, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СтруктурнаяЕдиницаПриИзменении(Элемент)
	
	Если СтруктурнаяЕдиница <> СтараяСтруктурнаяЕдиница И ЗагружаемыеСНТ.Количество() <> 0 Тогда 
		
		ТекстВопроса = НСтр(
			"ru = 'Таблица загруженных сопроводительных накладных на товары будет очищена.
			|Продолжить?'");
			
		ОчисткаТаблицыСНТЗавершение = Новый ОписаниеОповещения("ОчисткаТаблицыСНТЗавершение", ЭтаФорма);
		ПоказатьВопрос(ОчисткаТаблицыСНТЗавершение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена,, КодВозвратаДиалога.Отмена);
		
	ИначеЕсли ЗагружаемыеСНТ.Количество() = 0 Тогда
		
		СтараяСтруктурнаяЕдиница = СтруктурнаяЕдиница;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчисткаТаблицыСНТЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		СтруктурнаяЕдиница = СтараяСтруктурнаяЕдиница;
		Возврат;
	КонецЕсли;
	
	ЗагружаемыеСНТ.Очистить();
	СтараяСтруктурнаяЕдиница = СтруктурнаяЕдиница;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьНедоступныеРеквизитыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		СтараяСтруктурнаяЕдиница = СтруктурнаяЕдиница;
		ЗагружаемыеСНТ.Очистить();
	Иначе
		СтруктурнаяЕдиница = СтараяСтруктурнаяЕдиница;
	КонецЕсли;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ

&НаКлиенте
Процедура ЗагружаемыеСНТВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = ЗагружаемыеСНТ.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если ТекущиеДанные <> Неопределено Тогда
				
		Если Поле.Имя = "ЗагружаемыеСНТСНТ" Тогда
			
			ОткрытьСНТ();
			
		ИначеЕсли Поле.Имя <> "ЗагружаемыеСНТПометка" Тогда
			
			ПредварительныйПросмотрНаКлиенте();			
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагружаемыеСНТПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Если НЕ Копирование Тогда
		ЗагрузитьИзФайловНаКлиенте();	
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗагрузитьИзФайлов(Команда)
	
	Если ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда 
		ЗагрузитьИзФайловНаКлиенте();
	Иначе
		ТекстСообщения = НСтр("ru = 'Поле ""[СтруктурнаяЕдиница]"" не заполнено.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "[СтруктурнаяЕдиница]", Элементы.СтруктурнаяЕдиница.Заголовок);
		ЭСФКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОбновитьДокументыСНТ(Команда)
	
	МассивПомеченныхСтрок = МассивПомеченныхСтрокТаблицыЗагружаемыеСНТ();
	Если МассивПомеченныхСтрок.Количество() = 0 Тогда
		ЭСФКлиентСервер.СообщитьПользователю(НСтр("ru = 'Выберите хотя бы одну СНТ для создания и обновления.'"));
		Возврат;
	КонецЕсли;	
	
	ТекстВопроса = НСтр("ru = 'Создать новые и обновить существующие СНТ?'");
	СоздатьОбновитьДокументыСНТЗавершение = Новый ОписаниеОповещения("СоздатьОбновитьДокументыСНТЗавершение", ЭтаФорма);
	ПоказатьВопрос(СоздатьОбновитьДокументыСНТЗавершение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьОбновитьДокументыСНТЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		
		СоздатьОбновитьДокументыСНТНаСервере();
		
		СНТКлиент.ОповеститьФормы(СНТКлиентСервер.ИмяСобытияЗаписьСНТ());
		
		ЭСФКлиентСервер.СообщитьПользователю(НСтр("ru = 'Создание новых и обновление существующих СНТ успешно завершено.'"));
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СоздатьОбновитьДокументыСНТНаСервере()

	ОбработкаОбменСНТ = СНТСерверПовтИсп.ОбработкаОбменСНТ();
	
	ТабЗначЗагружаемыеСНТ = РеквизитФормыВЗначение("ЗагружаемыеСНТ");
	
	ОбработкаОбменСНТ.СоздатьОбновитьЗагружаемыеСНТ(СтруктурнаяЕдиница, ТабЗначЗагружаемыеСНТ);	
	
	ЗначениеВРеквизитФормы(ТабЗначЗагружаемыеСНТ, "ЗагружаемыеСНТ");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого СтрокаЗагружаемыеСНТ Из ЗагружаемыеСНТ Цикл 
		СтрокаЗагружаемыеСНТ.Пометка = Истина;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого СтрокаЗагружаемыеСНТ Из ЗагружаемыеСНТ Цикл 
		СтрокаЗагружаемыеСНТ.Пометка = Ложь;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСозданныйСНТ(Команда)
	
	ОткрытьСНТ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотр(Команда)
	
	ПредварительныйПросмотрНаКлиенте();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ЗагрузитьИзФайловНаКлиенте()
	
	#Если ВебКлиент Тогда
		
	АдресФайла = "";
	ВыбранноеИмяФайла = "";
	ПомещениеФайлаЗавершение = Новый ОписаниеОповещения("ЗагрузитьИзФайловНаВебКлиентеЗавершение", ЭтотОбъект);
	НачатьПомещениеФайла(ПомещениеФайлаЗавершение,АдресФайла, ,,Новый УникальныйИдентификатор)
	
	#Иначе
		
		МассивАдресовЗагружаемыхФайлов = МассивАдресовДвоичныхДанныхЗагружаемыхФайлов();
		
		Если МассивАдресовЗагружаемыхФайлов.Количество() <> 0 Тогда
			ЗагрузитьИзФайловНаСервере(МассивАдресовЗагружаемыхФайлов);
		КонецЕсли;
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайловНаВебКлиентеЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		МассивФайлов = Новый Массив;
		
		Файл = ЭСФВызовСервера.ИнфомацияПоФайлу(ВыбранноеИмяФайла);
		
		Если Файл.Расширение = ".xml" ИЛИ Файл.Расширение = ".txt" Тогда
			ОписаниеФайла = Новый Структура("Имя, Адрес", ВыбранноеИмяФайла, Адрес);
			МассивФайлов.Добавить(ОписаниеФайла);	
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Выбран некорректный файл. Необходимо выбрать файл с расширением ""xml"".'"));
		КонецЕсли;
		
		Если МассивФайлов.Количество() <> 0 Тогда
			ЗагрузитьИзФайловНаСервере(МассивФайлов);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьИзФайловНаСервере(Знач МассивАдресовЗагружаемыхФайлов)
	
	ОбработкаОбменСНТ = СНТСерверПовтИсп.ОбработкаОбменСНТ();
	
	ТабЗначЗагружаемыеСНТ = РеквизитФормыВЗначение("ЗагружаемыеСНТ");
	
	ОбработкаОбменСНТ.ЗаполнитьТаблицуЗагружаемыеСНТ(МассивАдресовЗагружаемыхФайлов, СтруктурнаяЕдиница, ТабЗначЗагружаемыеСНТ);
	
	ЗначениеВРеквизитФормы(ТабЗначЗагружаемыеСНТ, "ЗагружаемыеСНТ");
	
КонецПроцедуры

// Возвращает массив, каждый элемент которого содержит имя и данные файла.
//
// Возвращаемое значение:
//  Массив - Описание выбранных файлов. Каждый элемент массива:
//   Структура
//    |- Имя - Строка - Полное имя файла на клиенте, включая расширение.
//    |- Адрес - Строка - Адрес двоичных данных файла во временном хранилище.
//
&НаКлиенте
Функция МассивАдресовДвоичныхДанныхЗагружаемыхФайлов()
	
	МассивФайлов = Новый Массив;
	
	#Если ВебКлиент Тогда
		
		АдресФайла = "";
		ВыбранноеИмяФайла = "";
		
		Если ПоместитьФайл(АдресФайла, , ВыбранноеИмяФайла, , Новый УникальныйИдентификатор) Тогда 
			
			Файл = ЭСФВызовСервера.ИнфомацияПоФайлу(ВыбранноеИмяФайла);
			
			Если Файл.Расширение = ".xml" ИЛИ Файл.Расширение = ".txt" Тогда
				ОписаниеФайла = Новый Структура("Имя, Адрес", ВыбранноеИмяФайла, АдресФайла);
				МассивФайлов.Добавить(ОписаниеФайла);	
			Иначе
				ПоказатьПредупреждение(, НСтр("ru = 'Выбран некорректный файл. Необходимо выбрать файл с расширением ""xml"".'"));
			КонецЕсли;
			
		КонецЕсли;
		
	#Иначе
		
		ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);	
		ДиалогВыбораФайла.Фильтр = "Пакет сопроводительных накладных на товары (*.xml)|*.xml"; 
		ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
		ДиалогВыбораФайла.МножественныйВыбор = Истина;
		
		Если ДиалогВыбораФайла.Выбрать() Тогда
			Для Каждого ПолноеИмяФайла Из ДиалогВыбораФайла.ВыбранныеФайлы Цикл
				ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяФайла);
				АдресФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные); 
				ОписаниеФайла = Новый Структура("Имя, Адрес", ПолноеИмяФайла, АдресФайла);
				МассивФайлов.Добавить(ОписаниеФайла);	
			КонецЦикла;
		КонецЕсли;	
		
	#КонецЕсли
	
	Возврат МассивФайлов;
	
КонецФункции

&НаКлиенте
Функция МассивПомеченныхСтрокТаблицыЗагружаемыеСНТ()
	
	МассивПомеченныхСтрок = Новый Массив;
	
	Для Каждого СтрокаЗагружаемыеСНТ Из ЗагружаемыеСНТ Цикл
		Если СтрокаЗагружаемыеСНТ.Пометка Тогда
			МассивПомеченныхСтрок.Добавить(СтрокаЗагружаемыеСНТ);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивПомеченныхСтрок;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьСНТ()
	
	ТекущиеДанные = Элементы.ЗагружаемыеСНТ.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(ТекущиеДанные.СНТ) Тогда
			ПоказатьЗначение(, ТекущиеДанные.СНТ);
		Иначе
			ЭСФКлиентСервер.СообщитьПользователю(НСтр("ru = 'Для загружаемого СНТ в информационной базе отсутствует созданный СНТ.'"));			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрНаКлиенте()
	
	ТекущиеДанные = Элементы.ЗагружаемыеСНТ.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда 
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
		ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
		
		Реквизиты = Новый Структура;
		Реквизиты.Вставить("Организация", ТекущиеДанные.Организация);
		Реквизиты.Вставить("СтруктурноеПодразделение", ТекущиеДанные.СтруктурноеПодразделение);
		Реквизиты.Вставить("Состояние", ТекущиеДанные.Состояние);
		Реквизиты.Вставить("Направление", ТекущиеДанные.Направление);
		
		Основание = Новый Структура;
		Основание.Вставить("ПросмотрЗагружаемогоСНТ", Истина);
		Основание.Вставить("XML", ТекущиеДанные.XML);
		Основание.Вставить("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
		Основание.Вставить("Реквизиты", Реквизиты);
		
		ПараметрыФормы.Вставить("Основание", Основание);
		
		ОткрытьФорму("Документ.СНТ.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, ТекущиеДанные.Идентификатор);
		
	КонецЕсли;
	
КонецПроцедуры
