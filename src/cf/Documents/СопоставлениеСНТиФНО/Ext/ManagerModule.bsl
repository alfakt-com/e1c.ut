#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ДляВызоваИзДругихПодсистем

Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	СНТСерверПереопределяемый.ПриЗаполненииОграниченияДоступа(Ограничение, "СопоставлениеСНТиФНО");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаполнитьНаименованиеТовараПоФНО(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	Если Параметры.Свойство("МассивОбработанныхСопоставлений") Тогда
		МассивОбработанныхСопоставлений = Параметры.МассивОбработанныхСопоставлений;
	Иначе
		МассивОбработанныхСопоставлений = Новый Массив;
		Параметры.Вставить("МассивОбработанныхСопоставлений", МассивОбработанныхСопоставлений);
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	СопоставлениеСНТиФНО.Ссылка КАК Ссылка
		|
		|ИЗ
		|	Документ.СопоставлениеСНТиФНО КАК СопоставлениеСНТиФНО
		|ГДЕ
		|	НЕ СопоставлениеСНТиФНО.Ссылка В (&МассивОбработанныхСопоставлений)
		|
		|УПОРЯДОЧИТЬ ПО
		|	СопоставлениеСНТиФНО.Дата
		|ИТОГИ ПО
		|	СопоставлениеСНТиФНО.Ссылка
		|";
	
	Запрос.УстановитьПараметр("МассивОбработанныхСопоставлений", МассивОбработанныхСопоставлений);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	ОбъектовОбработано = 0;
	
	Пока Выборка.Следующий() Цикл
		
		МассивОбработанныхСопоставлений.Добавить(Выборка.Ссылка);
		СопоставлениеСНТиФНО = Выборка.Ссылка.ПолучитьОбъект();
		
		СопоставлениеСНТиФНО.ОбменДанными.Загрузка = Истина;
		
		Для Каждого СтрокаТовары Из СопоставлениеСНТиФНО.ТоварыСНТ Цикл
			Если Не ЗначениеЗаполнено(СтрокаТовары.ТоварНаименованиеВРамкахТС) Тогда
				ТоварНаименованиеВРамкахТС = СтрокаТовары.ИсточникПроисхожденияПолучатель.ТоварНаименованиеВРамкахТС;
				СтрокаТовары.ТоварНаименованиеВРамкахТС = ТоварНаименованиеВРамкахТС;
			КонецЕсли;
		КонецЦикла;
		
		Попытка
			СопоставлениеСНТиФНО.Записать();
			ОбъектовОбработано = ОбъектовОбработано + 1;
		Исключение
			ТекстСообщения = НСтр("ru='При записи документа %1 произошла ошибка: %2'");
			ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, СопоставлениеСНТиФНО, ОписаниеОшибки());
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбъектовОбработано;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
