&НаКлиенте
Процедура ВыбратьПериод(Команда)
		
	ПараметрыОтбора    = Новый Структура("НачалоПериода, КонецПериода, ВыборКварталов", Объект.НачалоПериода, Объект.КонецПериода, Ложь);	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыОтбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, РезультатВыбора, "НачалоПериода,КонецПериода");
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьБонусыНаСервере()
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ДокументЗаказа.Дизайнер КАК Дизайнер,
	                      |	ДокументРеализации.Менеджер КАК Менеджер,
	                      |	ДокументРеализации.Подразделение КАК Подразделение,
	                      |	ТоварыКОтгрузке.ДокументОтгрузки КАК ДокументЗаказаТовара,
	                      |	ТоварыКОтгрузке.Регистратор КАК ДокументДвиженияТовара,
	                      |	ТоварыКОтгрузке.Номенклатура КАК Номенклатура,
	                      |	ТоварыРеализации.ВидЦены КАК ВидЦены,
	                      |	ТоварыРеализации.Цена * ТоварыКОтгрузке.КОформлениюРасход КАК СуммаВыручки,
	                      |	ЕСТЬNULL(ТоварыРеализации.ПроцентРучнойСкидки, 0) + ЕСТЬNULL(ТоварыРеализации.ПроцентАвтоматическойСкидки, 0) КАК ПроцентСкидки,
	                      |	ДокументЗаказа.Дата КАК ДатаЗаказаТовара,
	                      |	ТоварыКОтгрузке.Период КАК ДатаДвиженияТовара,
	                      |	ТоварыПродажи.ТипНоменклатуры КАК ТипНоменклатуры
	                      |ПОМЕСТИТЬ ОсновныеДанные
	                      |ИЗ
	                      |	РегистрНакопления.ТоварыКОтгрузке.Обороты(&НачалоПериода, &КонецПериода, Регистратор, ) КАК ТоварыКОтгрузке
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДокументЗаказа
	                      |		ПО ТоварыКОтгрузке.ДокументОтгрузки = ДокументЗаказа.Ссылка
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК ТоварыРеализации
	                      |		ПО ТоварыКОтгрузке.Регистратор = ТоварыРеализации.Ссылка
	                      |			И ТоварыКОтгрузке.Номенклатура = ТоварыРеализации.Номенклатура
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДокументРеализации
	                      |		ПО ТоварыКОтгрузке.Регистратор = ДокументРеализации.Ссылка
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТоварыПродажи
	                      |		ПО ТоварыКОтгрузке.Номенклатура = ТоварыПродажи.Ссылка
	                      |ГДЕ
	                      |	ТИПЗНАЧЕНИЯ(ТоварыКОтгрузке.Регистратор) = ТИП(Документ.РеализацияТоваровУслуг)
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	ДокументЗаказа.Дизайнер,
	                      |	РеализацияТоваров.Менеджер,
	                      |	РеализацияТоваров.Подразделение,
	                      |	РеализацияТоваров.ЗаказКлиента,
	                      |	ТоварыКПоступлению.Регистратор,
	                      |	ТоварыКПоступлению.Номенклатура,
	                      |	ТоварыВозврата.ВидЦеныСебестоимости,
	                      |	ТоварыВозврата.Цена * ТоварыКПоступлению.КОформлениюПоступленийПоОрдерамОборот,
	                      |	0,
	                      |	ДокументЗаказа.Дата,
	                      |	ТоварыКПоступлению.Период,
	                      |	ТоварыПродажи.ТипНоменклатуры
	                      |ИЗ
	                      |	РегистрНакопления.ТоварыКПоступлению.Обороты(&НачалоПериода, &КонецПериода, Регистратор, ХозяйственнаяОперация = &ВозвратТоваровОтКлиента) КАК ТоварыКПоступлению
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваров
	                      |			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваров
	                      |				ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДокументЗаказа
	                      |				ПО РеализацияТоваров.ЗаказКлиента = ДокументЗаказа.Ссылка
	                      |			ПО ВозвратТоваров.ДокументРеализации = РеализацияТоваров.Ссылка
	                      |		ПО ТоварыКПоступлению.Регистратор = ВозвратТоваров.Ссылка
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ТоварыВозврата
	                      |		ПО ТоварыКПоступлению.Регистратор = ТоварыВозврата.Ссылка
	                      |			И ТоварыКПоступлению.Номенклатура = ТоварыВозврата.Номенклатура
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТоварыПродажи
	                      |		ПО ТоварыКПоступлению.Номенклатура = ТоварыПродажи.Ссылка
	                      |ГДЕ
	                      |	ТИПЗНАЧЕНИЯ(ТоварыКПоступлению.Регистратор) = ТИП(Документ.ВозвратТоваровОтКлиента)
	                      |
	                      |ИНДЕКСИРОВАТЬ ПО
	                      |	Номенклатура
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ДопРеквизиты.Ссылка КАК Номенклатура,
	                      |	ДопРеквизиты.Значение КАК КатегорияТоваров
	                      |ПОМЕСТИТЬ СписокКатегорийТоваров
	                      |ИЗ
	                      |	Справочник.Номенклатура.ДополнительныеРеквизиты КАК ДопРеквизиты
	                      |ГДЕ
	                      |	ДопРеквизиты.Ссылка В
	                      |			(ВЫБРАТЬ
	                      |				ОсновныеДанные.Номенклатура
	                      |			ИЗ
	                      |				ОсновныеДанные)
	                      |	И ДопРеквизиты.Свойство = &СвойствоПВХ
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ДопРеквизиты.Ссылка,
	                      |	ДопРеквизиты.Значение
	                      |
	                      |ИНДЕКСИРОВАТЬ ПО
	                      |	Номенклатура
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ШкалаБонусныхПроцентов.КатегорияТовара КАК КатегорияТовара,
	                      |	ШкалаБонусныхПроцентов.ВидРасчетаБонуса КАК ВидРасчетаБонуса,
	                      |	ШкалаБонусныхПроцентов.НижнийПорогСкидкиДляКлиента КАК НижнийПорогСкидкиДляКлиента,
	                      |	ШкалаБонусныхПроцентов.ВерхнийПорогСкидкиДляКлиента КАК ВерхнийПорогСкидкиДляКлиента,
	                      |	ШкалаБонусныхПроцентов.БонусныйПроцент КАК БонусныйПроцент
	                      |ПОМЕСТИТЬ ШкалаБонусов
	                      |ИЗ
	                      |	РегистрСведений.ШкалаБонусныхПроцентовОтДизайнерскихПродаж.СрезПоследних КАК ШкалаБонусныхПроцентов
	                      |ГДЕ
	                      |	ШкалаБонусныхПроцентов.ТипРасчетаБонуса = &ТипРасчетаБонуса
	                      |	И ШкалаБонусныхПроцентов.КатегорияТовара В
	                      |			(ВЫБРАТЬ
	                      |				СписокКатегорийТоваров.КатегорияТоваров
	                      |			ИЗ
	                      |				СписокКатегорийТоваров)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ШкалаБонусныхПроцентов.КатегорияТовара,
	                      |	ШкалаБонусныхПроцентов.БонусныйПроцент,
	                      |	ШкалаБонусныхПроцентов.НижнийПорогСкидкиДляКлиента,
	                      |	ШкалаБонусныхПроцентов.ВерхнийПорогСкидкиДляКлиента,
	                      |	ШкалаБонусныхПроцентов.ВидРасчетаБонуса
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ОсновныеДанные.Дизайнер КАК Дизайнер,
	                      |	ОсновныеДанные.Менеджер КАК Менеджер,
	                      |	ОсновныеДанные.Подразделение КАК Подразделение,
	                      |	ОсновныеДанные.ДокументЗаказаТовара КАК ДокументЗаказаТовара,
	                      |	ОсновныеДанные.ДокументДвиженияТовара КАК ДокументДвиженияТовара,
	                      |	СписокКатегорийТоваров.КатегорияТоваров КАК КатегорияТоваров,
	                      |	ОсновныеДанные.Номенклатура КАК Номенклатура,
	                      |	ОсновныеДанные.ВидЦены КАК ВидЦены,
	                      |	ОсновныеДанные.ПроцентСкидки КАК ПроцентСкидки,
	                      |	ОсновныеДанные.СуммаВыручки КАК СуммаВыручки,
	                      |	ОсновныеДанные.ДатаЗаказаТовара КАК ДатаЗаказаТовара,
	                      |	ОсновныеДанные.ДатаДвиженияТовара КАК ДатаДвиженияТовара
	                      |ПОМЕСТИТЬ ИтоговыеДанные
	                      |ИЗ
	                      |	ОсновныеДанные КАК ОсновныеДанные
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ СписокКатегорийТоваров КАК СписокКатегорийТоваров
	                      |		ПО ОсновныеДанные.Номенклатура = СписокКатегорийТоваров.Номенклатура
	                      |ГДЕ
	                      |	ОсновныеДанные.Дизайнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	                      |	И ОсновныеДанные.ТипНоменклатуры <> &Услуга
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ИтоговыеДанные.Дизайнер КАК Дизайнер,
	                      |	ИтоговыеДанные.Менеджер КАК Менеджер,
	                      |	ИтоговыеДанные.Подразделение КАК Подразделение,
	                      |	ИтоговыеДанные.ДокументЗаказаТовара КАК Заказ,
	                      |	ИтоговыеДанные.ДокументДвиженияТовара КАК Накладная,
	                      |	ИтоговыеДанные.КатегорияТоваров КАК КатегорияТовара,
	                      |	ИтоговыеДанные.Номенклатура КАК Номенклатура,
	                      |	ИтоговыеДанные.ВидЦены КАК ВидЦены,
	                      |	ИтоговыеДанные.ПроцентСкидки КАК ПроцентСкидки,
	                      |	ИтоговыеДанные.СуммаВыручки КАК СуммаВыручки,
	                      |	ШкалаБонусов.БонусныйПроцент / 100 * ИтоговыеДанные.СуммаВыручки КАК СуммаБонуса
	                      |ИЗ
	                      |	ИтоговыеДанные КАК ИтоговыеДанные
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ШкалаБонусов КАК ШкалаБонусов
	                      |		ПО ИтоговыеДанные.КатегорияТоваров = ШкалаБонусов.КатегорияТовара
	                      |			И ИтоговыеДанные.ВидЦены = ШкалаБонусов.ВидРасчетаБонуса
	                      |			И ИтоговыеДанные.ПроцентСкидки >= ШкалаБонусов.НижнийПорогСкидкиДляКлиента
	                      |			И ИтоговыеДанные.ПроцентСкидки <= ШкалаБонусов.ВерхнийПорогСкидкиДляКлиента");
	
	Запрос.УстановитьПараметр("НачалоПериода", Новый Граница(НачалоДня(Объект.НачалоПериода), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонецПериода",  Новый Граница(КонецДня(Объект.КонецПериода), ВидГраницы.Включая));	
	Запрос.УстановитьПараметр("ВозвратТоваровОтКлиента", Перечисления.ХозяйственныеОперации.ВозвратТоваровОтКлиента);	
	Запрос.УстановитьПараметр("СвойствоПВХ", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ГруппаТовара"));
	Запрос.УстановитьПараметр("Услуга",      Перечисления.ТипыНоменклатуры.Услуга);
	Запрос.УстановитьПараметр("ТипРасчетаБонуса", Объект.ТипРасчетаБонуса);
	
 	Объект.Бонусы.Загрузить(Запрос.Выполнить().Выгрузить());   	
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьБонусы()
	
	Если Объект.Организация.Пустая() Тогда
		Возврат
	КонецЕсли;
	
	Если Объект.Бонусы.Количество()>0 тогда
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект);	
  		ПоказатьВопрос(Оповещение, "Табличная часть расчетов будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Иначе
		РассчитатьБонусыНаСервере();
	КонецЕсли;	
	
КонецПроцедуры
 
&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
 
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат
    КонецЕсли;	
	
	РассчитатьБонусыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	ОчиститьТаблицуРасчетаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	ОчиститьТаблицуРасчетаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТаблицуРасчетаНаСервере()
	
	Объект.Бонусы.Очистить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция БонусыПроцентБонусаПриИзмененииНаСервере(ПроцентБонуса, СуммаВыручки)
	Возврат (ПроцентБонуса / 100) * СуммаВыручки;
КонецФункции

&НаКлиенте
Процедура БонусыПроцентБонусаПриИзменении(Элемент)
	
	ТекДанные = Элементы.Бонусы.ТекущиеДанные;
	
	Если ТекДанные = Неопределено 
		Или ТекДанные.ПроцентБонуса = 0 Тогда 
		Возврат
	КонецЕсли;
	
	ТекДанные.СуммаБонуса = БонусыПроцентБонусаПриИзмененииНаСервере(ТекДанные.ПроцентБонуса, ТекДанные.СуммаВыручки);
	
КонецПроцедуры







//&НаСервере
//Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
//	
//	Если НЕ Объект.Ссылка.Пустая() и Объект.Выплачено тогда
//		
//		Этаформа.Доступность = Ложь;
//			
//	КонецЕсли; 	
//	
//КонецПроцедуры


