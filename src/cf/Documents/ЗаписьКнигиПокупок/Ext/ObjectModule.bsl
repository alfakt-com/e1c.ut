#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов") Тогда
		ЗаполнитьДокументНаОснованииПриобретениеУслугПрочихАктивов(ДанныеЗаполнения);
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
		ЗаполнитьДокументНаОснованииАвансовыйОтчет(ДанныеЗаполнения);
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		ЗаполнитьДокументНаОснованииПриобретениеТоваровУслуг(ДанныеЗаполнения);
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
		ЗаполнитьДокументНаОснованииКорректировкаПриобретения(ДанныеЗаполнения);
	Иначе
		ЗаполнитьРеквизитыПоУмолчанию();
	КонецЕсли;
	
	ЗаполнитьЗависимыеОтКонтрагентаРеквизиты();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);

	Документы.ЗаписьКнигиПокупок.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);

	ПроведениеСерверУТ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДоходыИРасходыСервер.ОтразитьНДСЗаписиКнигиПокупок(ДополнительныеСвойства, Движения, Отказ);
	ДоходыИРасходыСервер.ОтразитьЖурналУчетаСчетовФактур(ДополнительныеСвойства, Движения, Отказ);
	ВзаиморасчетыСервер.ОтразитьСуммыДокументаВВалютеРегл(ДополнительныеСвойства, Движения, Отказ);
	
	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ ЗаписьДополнительногоЛиста Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КорректируемыйПериод");
	КонецЕсли; 
	
	Если НЕ ПредъявленСчетФактура Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НомерСчетаФактуры");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаСчетаФактуры");
	КонецЕсли; 
	
	Если НЕ ПредъявленСчетФактура Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодВидаОперации");
	КонецЕсли; 
	
	Если СпособКорректировкиНДС = Перечисления.СпособыКорректировкиНДС.Отложить
		ИЛИ СпособКорректировкиНДС = Перечисления.СпособыКорректировкиНДС.Заблокировать Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Ценности.ВидЦенности");
		МассивНепроверяемыхРеквизитов.Добавить("Ценности.Событие");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("Ценности.Номенклатура");
	КонецЕсли;
	
	Если НЕ ДатаОборотаВТабличнойЧасти Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Ценности.ДатаОборота");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	УчетНДСУТ.ПроверитьСовместимостьВидовЦенностейТабличнойЧасти(ЭтотОбъект, "Ценности", Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	ПроведениеСерверУТ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПроведениеСерверУТ.СформироватьЗаписиРегистровЗаданий(ЭтотОбъект);

	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ПроведениеСерверУТ.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);

	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	СуммаДокумента = Ценности.Итог("Сумма") + Ценности.Итог("СуммаНДС");
	
	Если НЕ ЗначениеЗаполнено(ДокументРасчетов) И ДокументРасчетов <> Неопределено Тогда
		ДокументРасчетов = Неопределено
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ВзаиморасчетыСервер.ЗаполнитьИдентификаторыСтрокВТабличнойЧасти(Ценности);
	КонецЕсли;
	
	Если НЕ ЗаписьДополнительногоЛиста И ЗначениеЗаполнено(КорректируемыйПериод) Тогда
		КорректируемыйПериод = '00010101'
	КонецЕсли;
	
КонецПроцедуры

#Область ЗаполнениеДокумента

Процедура ЗаполнитьРеквизитыПоУмолчанию()

	Ответственный	= Пользователи.ТекущийПользователь();
	Организация		= ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Валюта			= ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(Валюта);
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииПриобретениеУслугПрочихАктивов(Знач Основание)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПриобретениеУслугПрочихАктивов.Организация КАК Организация,
		|	ПриобретениеУслугПрочихАктивов.Контрагент КАК Контрагент,
		|	ПриобретениеУслугПрочихАктивов.Валюта КАК Валюта,
		|	ПриобретениеУслугПрочихАктивов.СуммаДокумента КАК СуммаДокумента,
		|	ПриобретениеУслугПрочихАктивов.Подразделение КАК Подразделение,
		|	""23"" КАК КодВидаОперации
		|ИЗ
		|	Документ.ПриобретениеУслугПрочихАктивов КАК ПриобретениеУслугПрочихАктивов
		|ГДЕ
		|	ПриобретениеУслугПрочихАктивов.Ссылка = &Основание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УчетныеПолитикиОрганизаций.ПрименяетсяУчетНДСПоФактическомуИспользованию КАК ПрименяетсяУчетНДСПоФактическомуИспользованию
		|ПОМЕСТИТЬ ВТ_УчетнаяПолитика
		|ИЗ
		|	Документ.ПриобретениеУслугПрочихАктивов КАК ПриобретениеУслугПрочихАктивов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&ДатаДокумента, ) КАК УчетнаяПолитикаОрганизацийСрезПоследних
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеПолитикиОрганизаций КАК УчетныеПолитикиОрганизаций
		|			ПО УчетныеПолитикиОрганизаций.Ссылка = УчетнаяПолитикаОрганизацийСрезПоследних.УчетнаяПолитика.Ссылка
		|		ПО ПриобретениеУслугПрочихАктивов.Организация = УчетнаяПолитикаОрганизацийСрезПоследних.Организация
		|ГДЕ
		|	ПриобретениеУслугПрочихАктивов.Ссылка = &Основание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриобретениеУслугПрочихАктивовРасходы.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ПриобретениеУслугПрочихАктивовРасходы.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ПриобретениеУслугПрочихАктивовРасходы.СуммаНДС) КАК УплаченныйНДС,
		|	СУММА(ПриобретениеУслугПрочихАктивовРасходы.СуммаСНДС - ПриобретениеУслугПрочихАктивовРасходы.СуммаНДС) КАК Сумма,
		|	ПриобретениеУслугПрочихАктивовРасходы.ВидОперацииПоступления КАК ВидОперацииПоступления,
		|	ВЫБОР
		|		КОГДА ВТ_УчетнаяПолитика.ПрименяетсяУчетНДСПоФактическомуИспользованию
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Общий)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Облагаемый)
		|	КОНЕЦ КАК ВидОборота
		|ИЗ
		|	Документ.ПриобретениеУслугПрочихАктивов.Расходы КАК ПриобретениеУслугПрочихАктивовРасходы,
		|	ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
		|ГДЕ
		|	ПриобретениеУслугПрочихАктивовРасходы.Ссылка = &Основание
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриобретениеУслугПрочихАктивовРасходы.СтавкаНДС,
		|	ПриобретениеУслугПрочихАктивовРасходы.ВидОперацииПоступления,
		|	ВЫБОР
		|		КОГДА ВТ_УчетнаяПолитика.ПрименяетсяУчетНДСПоФактическомуИспользованию
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Общий)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Облагаемый)
		|	КОНЕЦ");
		                                                                    
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.УстановитьПараметр("ДатаДокумента", Основание.Дата);
	
	Результат = Запрос.ВыполнитьПакет();
	ШапкаОснование = Результат[0].Выбрать();
	ШапкаОснование.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ШапкаОснование);
	
	ТабличнаяЧастьОснование = Результат[2];
	ЭтотОбъект.Ценности.Загрузить(ТабличнаяЧастьОснование.Выгрузить());
	
	ЭтотОбъект.ДокументРасчетов = Основание;
	ЭтотОбъект.ДокументОснование = Основание;
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииПриобретениеТоваровУслуг(Знач Основание)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПриобретениеТоваровУслуг.Организация КАК Организация,
		|	ПриобретениеТоваровУслуг.Контрагент КАК Контрагент,
		|	ПриобретениеТоваровУслуг.Валюта КАК Валюта,
		|	ПриобретениеТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
		|	ПриобретениеТоваровУслуг.Подразделение КАК Подразделение,
		|	""23"" КАК КодВидаОперации
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
		|ГДЕ
		|	ПриобретениеТоваровУслуг.Ссылка = &Основание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УчетныеПолитикиОрганизаций.ПрименяетсяУчетНДСПоФактическомуИспользованию КАК ПрименяетсяУчетНДСПоФактическомуИспользованию
		|ПОМЕСТИТЬ ВТ_УчетнаяПолитика
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&ДатаДокумента, ) КАК УчетнаяПолитикаОрганизацийСрезПоследних
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеПолитикиОрганизаций КАК УчетныеПолитикиОрганизаций
		|			ПО УчетныеПолитикиОрганизаций.Ссылка = УчетнаяПолитикаОрганизацийСрезПоследних.УчетнаяПолитика.Ссылка
		|		ПО ПриобретениеТоваровУслуг.Организация = УчетнаяПолитикаОрганизацийСрезПоследних.Организация
		|ГДЕ
		|	ПриобретениеТоваровУслуг.Ссылка = &Основание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ПриобретениеТоваровУслугТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ПриобретениеТоваровУслугТовары.СуммаНДС) КАК УплаченныйНДС,
		|	СУММА(ПриобретениеТоваровУслугТовары.СуммаСНДС - ПриобретениеТоваровУслугТовары.СуммаНДС) КАК Сумма,
		|	ПриобретениеТоваровУслугТовары.ВидОперацииПоступления КАК ВидОперацииПоступления,
		|	ВЫБОР
		|		КОГДА ВТ_УчетнаяПолитика.ПрименяетсяУчетНДСПоФактическомуИспользованию
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Общий)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Облагаемый)
		|	КОНЕЦ КАК ВидОборота
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары,
		|	ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка = &Основание
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриобретениеТоваровУслугТовары.СтавкаНДС,
		|	ПриобретениеТоваровУслугТовары.ВидОперацииПоступления,
		|	ВЫБОР
		|		КОГДА ВТ_УчетнаяПолитика.ПрименяетсяУчетНДСПоФактическомуИспользованию
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Общий)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Облагаемый)
		|	КОНЕЦ");
		
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.УстановитьПараметр("ДатаДокумента", Основание.Дата);
	
	Результат = Запрос.ВыполнитьПакет();
	ШапкаОснование = Результат[0].Выбрать();
	ШапкаОснование.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ШапкаОснование);
	
	ТабличнаяЧастьОснование = Результат[2];
	ЭтотОбъект.Ценности.Загрузить(ТабличнаяЧастьОснование.Выгрузить());
	
	ЭтотОбъект.ДокументРасчетов = Основание;
	ЭтотОбъект.ДокументОснование = Основание;
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииАвансовыйОтчет(Знач Основание)
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка         КАК ДокументОснование,
		|	ДанныеДокумента.Ссылка         КАК ДокументОплаты,
		|	ДанныеДокумента.Дата           КАК ДатаОплаты,
		|	ДанныеДокумента.Организация    КАК Организация,
		|	ДанныеДокумента.Валюта         КАК Валюта,
		|	ДанныеДокумента.Подразделение  КАК Подразделение,
		|	""23"" КАК КодВидаОперации // 23 Командировочные расходы по бланку строгой отчетности, п.7 статьи 171 НК
		|ИЗ
		|	Документ.АвансовыйОтчет КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Основание
		|	И НЕ ДанныеДокумента.Мультивалютный
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка         КАК ДокументОснование,
		|	ДанныеДокумента.Ссылка         КАК ДокументОплаты,
		|	ДанныеДокумента.Дата           КАК ДатаОплаты,
		|	ДанныеДокумента.Организация    КАК Организация,
		|	Расходы.Валюта                 КАК Валюта,
		|	ДанныеДокумента.Подразделение  КАК Подразделение,
		|	""23"" КАК КодВидаОперации // 23 Командировочные расходы по бланку строгой отчетности, п.7 статьи 171 НК
		|ИЗ
		|	Документ.АвансовыйОтчет КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		(ВЫБРАТЬ
		|				Расходы.Ссылка,
		|				МАКСИМУМ(Расходы.Валюта) КАК Валюта
		|			ИЗ
		|				Документ.АвансовыйОтчет.ПрочиеРасходы КАК Расходы
		|			ГДЕ
		|				Расходы.Ссылка = &Основание
		|			СГРУППИРОВАТЬ ПО
		|				Расходы.Ссылка
		|			ИМЕЮЩИЕ
		|				КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Расходы.Валюта) = 1
		|		) КАК Расходы
		|	ПО
		|		Расходы.Ссылка = ДанныеДокумента.Ссылка
		|	
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Основание
		|	И ДанныеДокумента.Мультивалютный
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	МАКСИМУМ(Расходы.Контрагент) КАК Контрагент
		|	
		|ИЗ
		|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК Расходы
		|ГДЕ
		|	Расходы.Ссылка = &Основание
		|	И Расходы.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
		|	И Расходы.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДСНеРК)
		|	И НЕ Расходы.Отменено
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Расходы.Контрагент) = 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)        КАК ВидЦенности,
		|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету) КАК Событие,
		|	Расходы.СтавкаНДС                  КАК СтавкаНДС,
		|	СУММА(Расходы.СуммаНДС)            КАК СуммаНДС,
		|	СУММА(Расходы.СуммаНДС)            КАК УплаченныйНДС,
		|	СУММА(Расходы.Сумма) - СУММА(Расходы.СуммаНДС) КАК Сумма
		|	
		|ИЗ
		|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК Расходы
		|ГДЕ
		|	Расходы.Ссылка = &Основание
		|	И Расходы.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
		|	И Расходы.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДСНеРК)
		|	И НЕ Расходы.Отменено
		|	И НЕ Расходы.Ссылка.Мультивалютный
		|
		|СГРУППИРОВАТЬ ПО
		|	Расходы.СтавкаНДС
		|");
		
	Запрос.УстановитьПараметр("Основание", Основание);
	
	Результат = Запрос.ВыполнитьПакет();
	РеквизитыОснования = Результат[0].Выбрать();
	РеквизитыОснования.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыОснования);
	
	СтрокаДокументыОплаты = ЭтотОбъект.ДокументыОплаты.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаДокументыОплаты, РеквизитыОснования);
	
	Если Не Результат[1].Пустой() Тогда
		
		Выборка = Результат[1].Выбрать();
		Выборка.Следующий();
		ЭтотОбъект.Контрагент = Выборка.Контрагент;
		
		ЭтотОбъект.Ценности.Загрузить(Результат[2].Выгрузить());
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ЗаполнитьДокументНаОснованииКорректировкаПриобретения(Знач Основание)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КорректировкаПриобретения.Организация КАК Организация,
		|	КорректировкаПриобретения.Контрагент КАК Контрагент,
		|	КорректировкаПриобретения.Валюта КАК Валюта,
		|	КорректировкаПриобретения.СуммаДокумента КАК СуммаДокумента,
		|	КорректировкаПриобретения.Подразделение КАК Подразделение,
		|	""23"" КАК КодВидаОперации
		|ИЗ
		|	Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
		|ГДЕ
		|	КорректировкаПриобретения.Ссылка = &Основание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УчетныеПолитикиОрганизаций.ПрименяетсяУчетНДСПоФактическомуИспользованию КАК ПрименяетсяУчетНДСПоФактическомуИспользованию
		|ПОМЕСТИТЬ ВТ_УчетнаяПолитика
		|ИЗ
		|	Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&ДатаДокумента, ) КАК УчетнаяПолитикаОрганизацийСрезПоследних
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеПолитикиОрганизаций КАК УчетныеПолитикиОрганизаций
		|			ПО УчетныеПолитикиОрганизаций.Ссылка = УчетнаяПолитикаОрганизацийСрезПоследних.УчетнаяПолитика.Ссылка
		|		ПО КорректировкаПриобретения.Организация = УчетнаяПолитикаОрганизацийСрезПоследних.Организация
		|ГДЕ
		|	КорректировкаПриобретения.Ссылка = &Основание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КорректировкаПриобретенияРасхождения.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(КорректировкаПриобретенияРасхождения.СуммаНДС) КАК СуммаНДС,
		|	СУММА(КорректировкаПриобретенияРасхождения.СуммаНДС) КАК УплаченныйНДС,
		|	СУММА(КорректировкаПриобретенияРасхождения.СуммаСНДС - КорректировкаПриобретенияРасхождения.СуммаНДС) КАК Сумма,
		|	КорректировкаПриобретенияРасхождения.ВидОперацииПоступления КАК ВидОперацииПоступления,
		|	ВЫБОР
		|		КОГДА ВТ_УчетнаяПолитика.ПрименяетсяУчетНДСПоФактическомуИспользованию
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Общий)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Облагаемый)
		|	КОНЕЦ КАК ВидОборота
		|ИЗ
		|	Документ.КорректировкаПриобретения.Расхождения КАК КорректировкаПриобретенияРасхождения,
		|	ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
		|ГДЕ
		|	КорректировкаПриобретенияРасхождения.Ссылка = &Основание
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаПриобретенияРасхождения.СтавкаНДС,
		|	КорректировкаПриобретенияРасхождения.ВидОперацииПоступления,
		|	ВЫБОР
		|		КОГДА ВТ_УчетнаяПолитика.ПрименяетсяУчетНДСПоФактическомуИспользованию
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Общий)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоРеализации.Облагаемый)
		|	КОНЕЦ");
		
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.УстановитьПараметр("ДатаДокумента", Основание.Дата);
	
	Результат = Запрос.ВыполнитьПакет();
	ШапкаОснование = Результат[0].Выбрать();
	ШапкаОснование.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ШапкаОснование);
	
	ТабличнаяЧастьОснование = Результат[2];
	ЭтотОбъект.Ценности.Загрузить(ТабличнаяЧастьОснование.Выгрузить());
	
	ЭтотОбъект.ДокументРасчетов = Основание;
	ЭтотОбъект.ДокументОснование = Основание;
КонецПроцедуры

Процедура 	ЗаполнитьЗависимыеОтКонтрагентаРеквизиты()
	
	Если ЗначениеЗаполнено(Контрагент)
		И ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		РеквизитыКонтрагента = Справочники.Контрагенты.РеквизитыКонтрагента(Контрагент, Дата);
		ИННКонтрагента = РеквизитыКонтрагента.ИНН;
		КППКонтрагента = РеквизитыКонтрагента.КПП;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
