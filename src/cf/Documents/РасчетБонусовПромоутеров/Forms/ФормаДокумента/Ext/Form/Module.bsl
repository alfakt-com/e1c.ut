
&НаСервере
Процедура РассчитатьНаСервере()
	
	Объект.Расчеты.Очистить();
	//порядок расчета бонусов по мотивационной программе промоутеров:
	//1. промоутерские
	
	//рассчитываем  дизайнерские бонусы
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ШкалаБонусныхПроцентовОтПромоутерскихПродаж.Контрагент КАК Контрагент,
	|	СУММА(ПродажиПромоутеров.СуммаПродаж * (ШкалаБонусныхПроцентовОтПромоутерскихПродаж.БонусныйПроцент / 100)) КАК СуммаБонусов,
	|	ШкалаБонусныхПроцентовОтПромоутерскихПродаж.ВидБонуса КАК ВидБонуса,
	|	СРЕДНЕЕ(ШкалаБонусныхПроцентовОтПромоутерскихПродаж.БонусныйПроцент) КАК БонусныйПроцент,
	|	СУММА(ПродажиПромоутеров.СуммаПродаж) КАК СуммаВыручки,
	|	ШкалаБонусныхПроцентовОтПромоутерскихПродаж.Должность КАК Должность
	|ПОМЕСТИТЬ втПред
	|ИЗ
	|	РегистрСведений.ШкалаБонусныхПроцентовОтПромоутерскихПродаж КАК ШкалаБонусныхПроцентовОтПромоутерскихПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПродажиПромоутеров КАК ПродажиПромоутеров
	|		ПО ШкалаБонусныхПроцентовОтПромоутерскихПродаж.Контрагент = ПродажиПромоутеров.Контрагент
	|			И ШкалаБонусныхПроцентовОтПромоутерскихПродаж.НижнийПорогОбъемаПродаж <= ПродажиПромоутеров.СуммаПродаж
	|			И ШкалаБонусныхПроцентовОтПромоутерскихПродаж.ВерхнийПорогОбъемаПродаж >= ПродажиПромоутеров.СуммаПродаж
	|			И ШкалаБонусныхПроцентовОтПромоутерскихПродаж.Период = ПродажиПромоутеров.Период
	|ГДЕ
	|	ШкалаБонусныхПроцентовОтПромоутерскихПродаж.Период = &ВыбМесяц
	|	И ШкалаБонусныхПроцентовОтПромоутерскихПродаж.ВидБонуса = &ВидБонусаПромоутерские
	|
	|СГРУППИРОВАТЬ ПО
	|	ШкалаБонусныхПроцентовОтПромоутерскихПродаж.Контрагент,
	|	ШкалаБонусныхПроцентовОтПромоутерскихПродаж.ВидБонуса,
	|	ШкалаБонусныхПроцентовОтПромоутерскихПродаж.Должность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПред.Контрагент КАК Контрагент,
	|	втПред.СуммаБонусов КАК СуммаБонусов,
	|	втПред.ВидБонуса КАК ВидБонуса,
	|	втПред.БонусныйПроцент КАК БонусныйПроцент,
	|	втПред.СуммаВыручки КАК СуммаВыручки,
	|	Пользователи.Ссылка КАК Промоутер
	|ПОМЕСТИТЬ втОбщ
	|ИЗ
	|	втПред КАК втПред
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО втПред.Должность = Пользователи.Должность
	|			И (НЕ Пользователи.ПометкаУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОбщ.Контрагент КАК Контрагент,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втОбщ.Промоутер) КАК КоличествоПромоутеров
	|ПОМЕСТИТЬ втРазличные
	|ИЗ
	|	втОбщ КАК втОбщ
	|
	|СГРУППИРОВАТЬ ПО
	|	втОбщ.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОбщ.Контрагент КАК Контрагент,
	|	втОбщ.СуммаБонусов / втРазличные.КоличествоПромоутеров КАК СуммаБонусов,
	|	втОбщ.ВидБонуса КАК ВидБонуса,
	|	втОбщ.БонусныйПроцент КАК БонусныйПроцент,
	|	втОбщ.СуммаВыручки / втРазличные.КоличествоПромоутеров КАК СуммаВыручки,
	|	втОбщ.Промоутер КАК Промоутер
	|ИЗ
	|	втОбщ КАК втОбщ
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРазличные КАК втРазличные
	|		ПО втОбщ.Контрагент = втРазличные.Контрагент";
	Запрос.УстановитьПараметр("ВыбМесяц", НачалоМесяца(Объект.МесяцРасчета));
	Запрос.УстановитьПараметр("ВидБонусаПромоутерские", Перечисления.ВидыБонусов.Промоутерские);
 
    тзБонусы = Запрос.Выполнить().Выгрузить();
	
	Объект.Расчеты.Загрузить(тзБонусы);   	
	
КонецПроцедуры

&НаКлиенте
Процедура Рассчитать()
	
	Если Объект.Расчеты.Количество()>0 тогда
		ДиалогСВопросом();
	Иначе
		РассчитатьНаСервере();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДиалогСВопросом()
 
    Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса",
      ЭтотОбъект);	
 
    ПоказатьВопрос(Оповещение,
        "Табличная часть расчетов будет очищена и документ будет записан. Продолжить?",
        РежимДиалогаВопрос.ДаНетОтмена,
        0, // таймаут в секундах
        КодВозвратаДиалога.Да, // (необ.) кнопка по умолчанию
        "Хороший вопрос" // (необ.) заголовок
    );    
 
КонецПроцедуры
 
&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
 
	Если Результат = КодВозвратаДиалога.Да Тогда
        РассчитатьНаСервере();
    КонецЕсли;	
 
КонецПроцедуры

&НаКлиенте
Процедура МесяцРасчетаПриИзменении(Элемент)
	
	Объект.Расчеты.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Объект.Ссылка.Пустая() и Объект.Выплачено тогда
		
		Этаформа.Доступность = Ложь;
			
	КонецЕсли; 	

КонецПроцедуры
