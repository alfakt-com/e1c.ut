////////////////////////////////////////////////////////////////////////////////
// ПрефиксацияОбъектовУТСобытия: модуль реализует функционал по префиксации объектов
//  поддерживаемой только в конфигурации УправлениеТорговлейДляКазахстана.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьПрефиксИнформационнойБазыИСклада(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	ПрефиксацияОбъектовСобытия.УстановитьПрефиксИнформационнойБазы(Источник, СтандартнаяОбработка, Префикс);
	
	РеквизитыПрефиксации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Склад, "Префикс");
	
	Если ЗначениеЗаполнено(РеквизитыПрефиксации)
		И ЗначениеЗаполнено(РеквизитыПрефиксации.Префикс) Тогда
		
		Префикс = Лев(Префикс, 2) + РеквизитыПрефиксации.Префикс + Прав(Префикс, СтрДлина(Префикс) - 2);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПрефиксИнформационнойБазыОрганизацииИСклада(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	ПрефиксацияОбъектовСобытия.УстановитьПрефиксИнформационнойБазыИОрганизации(Источник, СтандартнаяОбработка, Префикс);
	
	РеквизитыПрефиксации = "";
	Если Источник.Метаданные().Реквизиты.Найти("Склад") <> Неопределено Тогда
		РеквизитыПрефиксации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Склад, "Префикс");
	ИначеЕсли Источник.Метаданные().Реквизиты.Найти("СкладОтправитель") <> Неопределено Тогда
		РеквизитыПрефиксации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.СкладОтправитель, "Префикс");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыПрефиксации)
		И ЗначениеЗаполнено(РеквизитыПрефиксации.Префикс) Тогда
		
		Префикс = Лев(Префикс, 4) + РеквизитыПрефиксации.Префикс + Прав(Префикс, СтрДлина(Префикс) - 4);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПрефиксИнформационнойБазыОрганизацииИБанковскогоСчета(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	ПрефиксацияОбъектовСобытия.УстановитьПрефиксИнформационнойБазыИОрганизации(Источник, СтандартнаяОбработка, Префикс);
	
	РеквизитыПрефиксации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.БанковскийСчет, "Префикс");
	
	Если ЗначениеЗаполнено(РеквизитыПрефиксации)
		И ЗначениеЗаполнено(РеквизитыПрефиксации.Префикс) Тогда
		
		Префикс = Лев(Префикс, 4) + РеквизитыПрефиксации.Префикс + Прав(Префикс, СтрДлина(Префикс) - 4);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНомерДокументаПоДатеИСкладу(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ПрефиксацияОбъектовСобытия.ПроверитьНомерДокументаПоДате(Источник, Отказ, РежимЗаписи, РежимПроведения);
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	ИначеЕсли Источник.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПрефиксацииДоИзменения = "";
	РеквизитыПрефиксацииПослеИзменения = "";
	
	СкладСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "Склад");
	РеквизитыПрефиксацииДоИзменения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СкладСсылка, "Префикс");
	
	РеквизитыПрефиксацииПослеИзменения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Склад, "Префикс");
	
	ПрефиксДоИзменения = ?(
		ЗначениеЗаполнено(РеквизитыПрефиксацииДоИзменения)
		И ЗначениеЗаполнено(РеквизитыПрефиксацииДоИзменения.Префикс),
		РеквизитыПрефиксацииДоИзменения.Префикс,
		"");
		
	ПрефиксПослеИзменения = ?(
		ЗначениеЗаполнено(РеквизитыПрефиксацииПослеИзменения)
		И ЗначениеЗаполнено(РеквизитыПрефиксацииПослеИзменения.Префикс),
		РеквизитыПрефиксацииПослеИзменения.Префикс,
		"");
	
	Если ПрефиксДоИзменения <> ПрефиксПослеИзменения Тогда
		Источник.Номер = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНомерДокументаПоДатеОрганизацииИСкладу(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ПрефиксацияОбъектовСобытия.ПроверитьНомерДокументаПоДатеИОрганизации(Источник, Отказ, РежимЗаписи, РежимПроведения);
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	ИначеЕсли Источник.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПрефиксацииДоИзменения = "";
	РеквизитыПрефиксацииПослеИзменения = "";
	
	Если Источник.Метаданные().Реквизиты.Найти("Склад") <> Неопределено Тогда
		СкладСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "Склад");
		
		РеквизитыПрефиксацииДоИзменения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СкладСсылка, "Префикс");
		РеквизитыПрефиксацииПослеИзменения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Склад, "Префикс");
	ИначеЕсли Источник.Метаданные().Реквизиты.Найти("СкладОтправитель") <> Неопределено Тогда
		СкладСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "СкладОтправитель");
		
		РеквизитыПрефиксацииДоИзменения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СкладСсылка, "Префикс");
		РеквизитыПрефиксацииПослеИзменения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.СкладОтправитель, "Префикс");
	КонецЕсли;
	
	ПрефиксДоИзменения = ?(
		ЗначениеЗаполнено(РеквизитыПрефиксацииДоИзменения)
		И ЗначениеЗаполнено(РеквизитыПрефиксацииДоИзменения.Префикс),
		РеквизитыПрефиксацииДоИзменения.Префикс,
		"");
		
	ПрефиксПослеИзменения = ?(
		ЗначениеЗаполнено(РеквизитыПрефиксацииПослеИзменения)
		И ЗначениеЗаполнено(РеквизитыПрефиксацииПослеИзменения.Префикс),
		РеквизитыПрефиксацииПослеИзменения.Префикс,
		"");
	
	Если ПрефиксДоИзменения <> ПрефиксПослеИзменения Тогда
		Источник.Номер = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНомерДокументаПоДатеОрганизацииИБанковскомуСчету(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ПрефиксацияОбъектовСобытия.ПроверитьНомерДокументаПоДатеИОрганизации(Источник, Отказ, РежимЗаписи, РежимПроведения);
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	ИначеЕсли Источник.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПрефиксацииДоИзменения = "";
	РеквизитыПрефиксацииПослеИзменения = "";
	
	БанковскийСчетСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "БанковскийСчет");
	РеквизитыПрефиксацииДоИзменения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БанковскийСчетСсылка, "Префикс");
	
	РеквизитыПрефиксацииПослеИзменения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.БанковскийСчет, "Префикс");
	
	ПрефиксДоИзменения = ?(
		ЗначениеЗаполнено(РеквизитыПрефиксацииДоИзменения)
		И ЗначениеЗаполнено(РеквизитыПрефиксацииДоИзменения.Префикс),
		РеквизитыПрефиксацииДоИзменения.Префикс,
		"");
		
	ПрефиксПослеИзменения = ?(
		ЗначениеЗаполнено(РеквизитыПрефиксацииПослеИзменения)
		И ЗначениеЗаполнено(РеквизитыПрефиксацииПослеИзменения.Префикс),
		РеквизитыПрефиксацииПослеИзменения.Префикс,
		"");
	
	Если ПрефиксДоИзменения <> ПрефиксПослеИзменения Тогда
		Источник.Номер = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

