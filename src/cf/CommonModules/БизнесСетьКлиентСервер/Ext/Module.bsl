////////////////////////////////////////////////////////////////////////////////
// Подсистема "Бизнес-сеть".
// ОбщийМодуль.БизнесСетьКлиентСервер.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Конвертирует дату из формата UnixTime в тип Дата.
// Параметры:
//   Источник - Число - число в формате UnixTime, например 1405955187848899.
//
// Возвращаемое значение:
//   Дата - значение даты.
//
Функция ДатаИзUnixTime(Источник) Экспорт
	
	Возврат МестноеВремя(Дата(1970, 1, 1, 0 ,0, 0) + Источник / 1000)
	
КонецФункции

// Корректировка идентификаторов ИНН и КПП участника для сервиса, "0" если не заполнено.
//
// Параметры:
//   ИНН - Строка - ИНН участника.
//   КПП - Строка - КПП участника.
//   ПредставлениеУчастника - Строка - наименование участника.
//   Отказ - Булево - признак ошибки данных.
//   ТекстОшибки - Строка - текст возвращаемой ошибки.
//
// Возвращаемое значение:
//   Структура - результат корректировки:
//     * ИНН - Строка - ИНН участника.
//     * КПП - Строка - КПП участника.
//
Функция ИдентификаторыУчастника(Знач ИНН, Знач КПП, Знач Участник = Неопределено, Отказ = Неопределено,
	ТекстОшибки = "") Экспорт

	Идентификаторы = Новый Структура("ИНН, КПП");
	
	Идентификаторы.ИНН = СокрЛП(ИНН);
	Если ПустаяСтрока(Идентификаторы.ИНН) Тогда
		Идентификаторы.ИНН = "0";
	КонецЕсли;
	
	Идентификаторы.КПП = СокрЛП(КПП);
	Если ПустаяСтрока(Идентификаторы.КПП) Тогда
		Идентификаторы.КПП = "0";
	КонецЕсли;
	
	Если (СтрДлина(Идентификаторы.КПП) <> 9 И СтрДлина(Идентификаторы.ИНН) <> 12)
		ИЛИ (СтрДлина(Идентификаторы.КПП) <> 1 И СтрДлина(Идентификаторы.ИНН) <> 10) Тогда
		
		Если ЗначениеЗаполнено(Участник) Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Некорректное значение ИНН ""%1"" или КПП ""%2"" для ""%3""'"), 
				ИНН, КПП, Участник);
		Иначе
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Некорректное значение ИНН ""%1"" или КПП ""%2""'"), 
				ИНН, КПП);
		КонецЕсли;
		Отказ = Истина;
		
	КонецЕсли;
	
	Возврат Идентификаторы;

КонецФункции

// Подготовка значения идентификатора сервиса.
//
// Параметры:
//   Значение - Строка - ИНН, КПП юридического лица в сервисе,, где "0" - пустое значение.
//
// Возвращаемое значение:
//   Строка - значение идентификатора сервиса.
//
Функция ЗначениеИдентификатора(Знач Значение) Экспорт
	
	Результат = Строка(Значение);
	Если Результат = "0" Тогда
		Результат = "";
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

// Описание идентификации организации контрагентов в сервисе 1С:Бизнес-сеть.
// 
// Возвращаемое значение:
//  Структура - параметры идентификации организации и контрагентов.
//   * Ссылка - СправочникСсылка - ссылка на организацию или контрагента.
//   * ИНН - Строка - ИНН.
//   * КПП - Строка - КПП.
//   * ЭтоОрганизация - Булево - является организацией.
//   * ЭтоКонтрагент - Булево - является контрагентом (по умолчанию).
//
Функция ОписаниеИдентификацииОрганизацииКонтрагентов() Экспорт
	
	Значение = Новый Структура;
	Значение.Вставить("Ссылка", Неопределено);
	Значение.Вставить("ИНН", "");
	Значение.Вставить("КПП", "");
	Значение.Вставить("ЭтоОрганизация", Ложь);
	Значение.Вставить("ЭтоКонтрагент",  Ложь);
	
	Возврат Значение;
	
КонецФункции

#КонецОбласти