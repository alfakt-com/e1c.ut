#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ЗаполнитьПризнакУчетаВС(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	Если Параметры.Свойство("МассивОбработанныхИП") Тогда
		МассивОбработанныхИП = Параметры.МассивОбработанныхИП;
	Иначе
		МассивОбработанныхИП = Новый Массив;
		Параметры.Вставить("МассивОбработанныхИП", МассивОбработанныхИП);
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ИП.Ссылка
	|ИЗ
	|	Справочник.ИсточникиПроисхождения КАК ИП
	|ГДЕ 
	|	ИП.ДополнительныйИдентификатор <> 0 
	|	И НЕ ИП.Ссылка В (&МассивОбработанныхИП)
	|УПОРЯДОЧИТЬ ПО
	|	ИП.ДополнительныйИдентификатор
	|";
	
	Запрос.УстановитьПараметр("МассивОбработанныхИП", МассивОбработанныхИП);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	ОбъектовОбработано = 0;
	Пока Выборка.Следующий() Цикл
		
		МассивОбработанныхИП.Добавить(Выборка.Ссылка);
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Попытка
			СправочникОбъект.ПризнакУчетаНаВиртуальномСкладе = Истина;
			СправочникОбъект.Записать();
		Исключение
			ТекстСообщения = НСтр("ru='При записи справочника %1 произошла ошибка: %2'");
			ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, СправочникОбъект, ОписаниеОшибки());
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
				
	КонецЦикла;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ОбработаноОбъектов + Выборка.Количество();
	
КонецПроцедуры

#КонецЕсли