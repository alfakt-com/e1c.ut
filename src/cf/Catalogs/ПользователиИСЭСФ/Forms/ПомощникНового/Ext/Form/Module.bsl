////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Скрыть заголовки страниц.
    Элементы.СтраницыПомощника.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.СтраницыПодвал.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		
	ПользовательИБ = ЭСФКлиентСерверПереопределяемый.ТекущийПользователь();
	ОбновитьСписокСтруктурныхЕдиницНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Контейнер = ЭСФКлиентСервер.КонтейнерМетодов();	
	Контейнер.ПриОткрытииФормы(ЭтаФорма, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаВыборСертификата;
	ОбновитьПанельКнопокНазадДалееЗакрыть();
	
	СертификатПолноеИмяФайла = НСтр("ru = 'Сертификат для входа в ИС ЭСФ.p12'");
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ПользовательИБ) Тогда
		Отказ = Истина;
		Сообщить(НСтр("ru = 'Поле ""Пользователь ИБ"" не заполнено.'"), СтатусСообщения.Внимание);	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИмяАутентификации) Тогда
		Отказ = Истина;
		Сообщить(НСтр("ru = 'Поле ""Имя для входа в ИС ЭСФ"" не заполнено, обычно оно должно содержать ИИН сотрудника.'"), СтатусСообщения.Внимание);	
	КонецЕсли;
	
	ЕстьПометка = Ложь;
	Для Каждого СтрокаСписка Из СписокСтруктурныхЕдиниц Цикл
		Если СтрокаСписка.Пометка Тогда
			ЕстьПометка = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЕстьПометка Тогда
		Отказ = Истина;
		Сообщить(НСтр("ru = 'Выберите хотя бы одну организацию, от имени которой будет работать создаваемый пользователь ИС ЭСФ.'"), СтатусСообщения.Внимание);	
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ГиперссылкаИСЭСФНажатие(Элемент)
	ПерейтиПоНавигационнойСсылке(ЭСФКлиентСервер.АдресВебПриложенияИСЭСФ());
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаЗарегистрироватьНажатие(Элемент)	
	ПерейтиПоНавигационнойСсылке(ЭСФКлиентСервер.АдресВебПриложенияИСЭСФ());
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ

&НаКлиенте
Процедура СписокСтруктурныхЕдиницПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокСтруктурныхЕдиницПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыбратьСертификат(Команда)
	
	КонтейнерМетодов = ЭСФКлиентСервер.КонтейнерМетодов();	
	
	Если НЕ КонтейнерМетодов.КриптопровайдерПодключается() Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаВыбораФайлаСертификата = Новый ОписаниеОповещения("ОбработкаВыбораФайлаСертификатаЗавершение", ЭтаФорма);
	НачатьПомещениеФайла(ОбработкаВыбораФайлаСертификата, , , , УникальныйИдентификатор); 	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораФайлаСертификатаЗавершение(ФайлВыбран, АдресКлюча, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если ФайлВыбран Тогда
		СертификатПолноеИмяФайла = ВыбранноеИмяФайла;
		ИнфомацияПоФайлу = ЭСФВызовСервера.ИнфомацияПоФайлу(ВыбранноеИмяФайла);
		Если ВРег(ИнфомацияПоФайлу.Расширение) = ".P12" Тогда
			КлючBase64 = КлючBase64(АдресКлюча);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Файл должен иметь расширение ""*.p12"".'"));
			Возврат;
		КонецЕсли;	
	Иначе
		Возврат;	
	КонецЕсли;
	
	// Получить пароль сертификата аутентификации.
	ДополнительныеПараметры = Новый Структура("КлючBase64", КлючBase64);	
	ОбработкаВводаПароляСертификатаЗавершение = Новый ОписаниеОповещения("ОбработкаВводаПароляСертификатаЗавершение", ЭтаФорма, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ОбменЭСФ.Форма.ВводПароля",,,,,, ОбработкаВводаПароляСертификатаЗавершение);	

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВводаПароляСертификатаЗавершение(Пароль, ДополнительныеПараметры) Экспорт
	
	Если Пароль = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	КонтейнерМетодов = ЭСФКлиентСервер.КонтейнерМетодов();	
	
	Если ЭСФВызовСервераПовтИсп.ВыполнятьКриптографическиеОперацииНаКлиенте() Тогда
		СертификатBase64 = ИзвлечьСертификатАутентификацииНаКлиенте(ДополнительныеПараметры.КлючBase64, Пароль);
		СвойстваСертификата = СвойстваСертификатаНаКлиенте(ДополнительныеПараметры.КлючBase64, Пароль);
	Иначе
		СертификатBase64 = ИзвлечьСертификатАутентификацииНаСервере(ДополнительныеПараметры.КлючBase64, Пароль);
		СвойстваСертификата = СвойстваСертификатаНаСервере(ДополнительныеПараметры.КлючBase64, Пароль);
	КонецЕсли;
			
	ИмяАутентификации = ЭСФКлиентСервер.ИмяАутентификации(СвойстваСертификата.ИИНСубъекта);
	ОписаниеСертификата = КонтейнерМетодов.ОписаниеСертификата(СвойстваСертификата);
	ОбновитьПанельКнопокНазадДалееЗакрыть();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	
	ОбновитьСписокСтруктурныхЕдиницНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого Строка Из СписокСтруктурныхЕдиниц Цикл
		Строка.Пометка = Истина;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого Строка Из СписокСтруктурныхЕдиниц Цикл
		Строка.Пометка = Ложь;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	ИндексТекущейСтраницы = Элементы.СтраницыПомощника.ПодчиненныеЭлементы.Индекс(Элементы.СтраницыПомощника.ТекущаяСтраница);
	
	Если ИндексТекущейСтраницы > 0 Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницыПомощника.ПодчиненныеЭлементы.Получить(ИндексТекущейСтраницы - 1);
	КонецЕсли;
	
	ОбновитьПанельКнопокНазадДалееЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаВыборСертификата И ПустаяСтрока(СертификатBase64) Тогда
		Сообщить(НСтр("ru = 'Выберите сертификат для входа в ИС ЭСФ.'"), СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
	
	МаксимальныйИндексСтраницы = Элементы.СтраницыПомощника.ПодчиненныеЭлементы.Количество() - 1;
	ИндексТекущейСтраницы = Элементы.СтраницыПомощника.ПодчиненныеЭлементы.Индекс(Элементы.СтраницыПомощника.ТекущаяСтраница);
	
	Если ИндексТекущейСтраницы < МаксимальныйИндексСтраницы Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницыПомощника.ПодчиненныеЭлементы.Получить(ИндексТекущейСтраницы + 1);
	КонецЕсли;
	
	ОбновитьПанельКнопокНазадДалееЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	ПользовательИСЭСФ = СоздатьПользователяИСЭСФИПрофилиИСЭСФ();
	
	Если НЕ ПользовательИСЭСФ.Пустая() Тогда
		
		ОповеститьОбИзменении(ПользовательИСЭСФ);
		ОповеститьОбИзменении(Тип("СправочникСсылка.ПрофилиИСЭСФ"));
		
		ПоказатьЗначение(, ПользовательИСЭСФ);
		
		ЭтаФорма.Закрыть(ПользовательИСЭСФ);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Извлечение сертификата

&НаКлиенте
Функция ИзвлечьСертификатАутентификацииНаКлиенте(Знач КлючBase64, Знач Пароль)
	
 	Возврат ИзвлечьСертификатАутентификации(ЭтаФорма, КлючBase64, Пароль);
	
КонецФункции

&НаСервере
Функция ИзвлечьСертификатАутентификацииНаСервере(Знач КлючBase64, Знач Пароль)
	
	Возврат ИзвлечьСертификатАутентификации(ЭтаФорма, КлючBase64, Пароль);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзвлечьСертификатАутентификации(Форма, Знач КлючBase64, Знач Пароль)
	
	Контейнер = ЭСФКлиентСервер.КонтейнерМетодов();
	СертификатBase64 = Контейнер.ОткрытыйСертификатBase64(КлючBase64, Пароль);
	Возврат СертификатBase64;
		
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Свойства сертификата

&НаКлиенте
Функция СвойстваСертификатаНаКлиенте(Знач КлючBase64, Знач Пароль)
	Возврат СвойстваСертификата(КлючBase64, Пароль);	
КонецФункции

&НаСервере
Функция СвойстваСертификатаНаСервере(Знач КлючBase64, Знач Пароль)
	Возврат СвойстваСертификата(КлючBase64, Пароль);	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СвойстваСертификата(Знач КлючBase64, Знач Пароль)
	
	Контейнер = ЭСФКлиентСервер.КонтейнерМетодов();
	СвойстваСертификата = Контейнер.СвойстваСертификата(КлючBase64, Пароль);
	Возврат СвойстваСертификата;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Прочие служебные процедуры и функции

&НаКлиенте
Процедура ОбновитьПанельКнопокНазадДалееЗакрыть()
		
	ИндексТекущейСтраницы = Элементы.СтраницыПомощника.ПодчиненныеЭлементы.Индекс(Элементы.СтраницыПомощника.ТекущаяСтраница);
	МаксимальныйИндексСтраницы = Элементы.СтраницыПомощника.ПодчиненныеЭлементы.Количество() - 1;
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаВыборСертификата Тогда
	
		Элементы.СтраницыПодвал.ТекущаяСтраница = Элементы.СтраницаДалее;
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
		
	Иначе
		
		Если ИндексТекущейСтраницы = 0 Тогда
			
			Элементы.СтраницыПодвал.ТекущаяСтраница = Элементы.СтраницаДалее;
			Элементы.Далее.КнопкаПоУмолчанию = Истина;
			
		ИначеЕсли ИндексТекущейСтраницы > 0 И ИндексТекущейСтраницы < МаксимальныйИндексСтраницы Тогда
			
			Элементы.СтраницыПодвал.ТекущаяСтраница = Элементы.СтраницаНазадДалее;
			Элементы.Далее2.КнопкаПоУмолчанию = Истина;
			
		Иначе // ИндексТекущейСтраницы = МаксимальныйИндексСтраницы
			
			Элементы.СтраницыПодвал.ТекущаяСтраница = Элементы.СтраницаГотово;	
			Элементы.Готово.КнопкаПоУмолчанию = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокСтруктурныхЕдиницНаСервере()
	
	СписокСтруктурныхЕдиниц.Очистить();
	
	ТаблицаСтруктурныхЕдиниц = ЭСФСервер.СтруктурныеЕдиницыДляСозданияПрофилейИСЭСФ();	
	
	Для Каждого СтрокаТаблицыСтруктурныхЕдиниц Из ТаблицаСтруктурныхЕдиниц Цикл
		НоваяСтрока = СписокСтруктурныхЕдиниц.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыСтруктурныхЕдиниц);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СоздатьПользователяИСЭСФИПрофилиИСЭСФ()
	
	Если ПроверитьЗаполнение() Тогда
		
		НачатьТранзакцию();
		
		Попытка
			
			// Создать пользователя ИС ЭСФ.
			
			ОбъектПользовательИСЭСФ = Справочники.ПользователиИСЭСФ.СоздатьЭлемент();
			ОбъектПользовательИСЭСФ.Наименование         = СокрЛП(ПользовательИБ);
			ОбъектПользовательИСЭСФ.Владелец             = ПользовательИБ;
			ОбъектПользовательИСЭСФ.ИмяАутентификации    = ИмяАутентификации;
			ОбъектПользовательИСЭСФ.ПарольАутентификации = ПарольАутентификации;
			ОбъектПользовательИСЭСФ.ОписаниеСертификата  = ОписаниеСертификата;
			
			Если НЕ ПустаяСтрока(СертификатBase64) Тогда
				ОбъектПользовательИСЭСФ.СертификатАутентификации = Новый ХранилищеЗначения(СертификатBase64);
			КонецЕсли;
			
			ОбъектПользовательИСЭСФ.Записать();			
			ПользовательИСЭСФ = ОбъектПользовательИСЭСФ.Ссылка;
			
			// Создать профили ИС ЭСФ.
			
			Для Каждого СтрокаСписка Из СписокСтруктурныхЕдиниц Цикл
				Если СтрокаСписка.Пометка Тогда 
					
					ОбъектПрофильИСЭСФ = Справочники.ПрофилиИСЭСФ.СоздатьЭлемент();
					ОбъектПрофильИСЭСФ.Наименование       = ОбъектПользовательИСЭСФ.Наименование + " (" + СтрокаСписка.Представление + ")";
					ОбъектПрофильИСЭСФ.Владелец           			= ПользовательИСЭСФ;
					ОбъектПрофильИСЭСФ.СтруктурнаяЕдиница 			= СтрокаСписка.СтруктурнаяЕдиница;
					ОбъектПрофильИСЭСФ.ИспользоватьДляСинхронизации = СтрокаСписка.ИспользоватьДляСинхронизации;
					
					ОбъектПрофильИСЭСФ.Записать();
					
				КонецЕсли;
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
		КонецПопытки;
		
	Иначе
		
		ПользовательИСЭСФ = Справочники.ПользователиИСЭСФ.ПустаяСсылка();
		
	КонецЕсли;
	
	Возврат ПользовательИСЭСФ;
	
КонецФункции

&НаСервереБезКонтекста
Функция КлючBase64(Знач АдресКлюча) Экспорт
	КлючДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресКлюча);
	КлючBase64 = Base64Строка(КлючДвоичныеДанные);
	Возврат КлючBase64;
КонецФункции