#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если (ДляОсвобожденногоОборота ИЛИ МестоРеализацииНеРК) И Ставка <> 0 Тогда
		Ставка = 0;
	КонецЕсли;
	
	Если НЕ Предопределенный Тогда
		ПроверитьДублиСтавокНДС(Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьДублиСтавокНДС(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Справочник.СтавкиНДС КАК СтавкиНДС
	|ГДЕ
	|	СтавкиНДС.Ставка = &Ставка
	|	И (СтавкиНДС.ДляОсвобожденногоОборота = &ДляОсвобожденногоОборота 
	|	  ИЛИ СтавкиНДС.МестоРеализацииНеРК = &МестоРеализацииНеРК)
	|	И СтавкиНДС.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Ставка", Ставка);
	Запрос.УстановитьПараметр("ДляОсвобожденногоОборота", ДляОсвобожденногоОборота);
	Запрос.УстановитьПараметр("МестоРеализацииНеРК", МестоРеализацииНеРК);

	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Ставка НДС с указанными параметрами уже существует. Дублирование ставок НДС запрещено.'"), Ссылка, , , Отказ);
	КонецЕсли;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
