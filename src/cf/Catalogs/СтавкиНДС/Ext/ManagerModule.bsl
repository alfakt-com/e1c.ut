#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает ставку НДС по переданному значению.
//
Функция СтавкаНДСПоЗначению(Ставка) Экспорт
	
	Если Ставка = 12 Тогда
		Возврат Справочники.СтавкиНДС.НДС12;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтавкиНДС.Ссылка КАК СтавкаНДС
	|ИЗ
	|	Справочник.СтавкиНДС КАК СтавкиНДС
	|ГДЕ
	|	(НЕ СтавкиНДС.ДляОсвобожденногоОборота И НЕ СтавкиНДС.МестоРеализацииНеРК)
	|	И СтавкиНДС.Ставка = &СтавкаНДС";
	
	Запрос.УстановитьПараметр("СтавкаНДС", Ставка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СтавкаНДС;
	Иначе
		Возврат ПредопределенноеЗначение("Справочник.СтавкиНДС.ПустаяСсылка");
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик обновления, запускаемый при первом запуске
// Процедура заполняет реквизиты предопределенных элементов справочника "СтавкиНДС"
//
Процедура ЗаполнитьРеквизитыПредопределенныхСтавокНДС() Экспорт
	
	// Ставка "Без НДС" для освобожденного оборота
	СтавкаНДС = Справочники.СтавкиНДС.БезНДС.ПолучитьОбъект();
	СтавкаНДС.Ставка = 0;
	СтавкаНДС.ДляОсвобожденногоОборота = Истина;
	СтавкаНДС.МестоРеализацииНеРК = Ложь;
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СтавкаНДС);
	
	// Ставка "Без НДС" для освобожденного оборота с местом реализации не РК
	СтавкаНДС = Справочники.СтавкиНДС.БезНДСНеРК.ПолучитьОбъект();
	СтавкаНДС.Ставка = 0;
	СтавкаНДС.МестоРеализацииНеРК = Истина;
	СтавкаНДС.ДляОсвобожденногоОборота = Ложь;
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СтавкаНДС);
	
	// Ставка "0%" со значением ставки 0
	СтавкаНДС = Справочники.СтавкиНДС.НДС0.ПолучитьОбъект();
	СтавкаНДС.Ставка = 0;
	СтавкаНДС.ДляОсвобожденногоОборота = Ложь;
	СтавкаНДС.МестоРеализацииНеРК = Ложь;
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СтавкаНДС);
	
	// Ставка "12%" со значением ставки 12
	СтавкаНДС = Справочники.СтавкиНДС.НДС12.ПолучитьОбъект();
	СтавкаНДС.Ставка = 12;
	СтавкаНДС.ДляОсвобожденногоОборота = Ложь;
	СтавкаНДС.МестоРеализацииНеРК = Ложь;
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СтавкаНДС);
	
КонецПроцедуры

Процедура ЗаполнитьСтавкуБезНДСНеРК() Экспорт
	
	// Ставка "Без НДС" для освобожденного оборота с местом реализации не РК
	СтавкаНДС = Справочники.СтавкиНДС.БезНДСНеРК.ПолучитьОбъект();
	СтавкаНДС.Ставка = 0;
	СтавкаНДС.ДляОсвобожденногоОборота = Ложь;
	СтавкаНДС.МестоРеализацииНеРК = Истина;
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СтавкаНДС);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
