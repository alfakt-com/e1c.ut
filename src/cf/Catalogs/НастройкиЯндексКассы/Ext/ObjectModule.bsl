#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НастройкаУникальна() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ОбработатьДополнительныеНастройки();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	РегистрыСведений.СтатусОбменовСЯндексКассой.ОбновитьЗначениеРеквизитаОрганизация(ЭтотОбъект.Ссылка,
		ЭтотОбъект.Организация);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НастройкаУникальна()
	
	Если ПометкаУдаления Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	НастройкиЯндексКассы.Наименование КАК НайденныйДоговорСЯндексКассой,
	|	""ИдентификаторМагазина"" КАК Реквизит
	|ИЗ
	|	Справочник.НастройкиЯндексКассы КАК НастройкиЯндексКассы
	|ГДЕ
	|	НастройкиЯндексКассы.Ссылка <> &Ссылка
	|	И НастройкиЯндексКассы.ИдентификаторМагазина = &ИдентификаторМагазина
	|	И НЕ НастройкиЯндексКассы.ПометкаУдаления
	|	И НЕ НастройкиЯндексКассы.Недействительна
	|	И НастройкиЯндексКассы.СДоговором = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиЯндексКассы.Наименование,
	|	""Организация""
	|ИЗ
	|	Справочник.НастройкиЯндексКассы КАК НастройкиЯндексКассы
	|ГДЕ
	|	НастройкиЯндексКассы.Ссылка <> &Ссылка
	|	И НастройкиЯндексКассы.Организация = &Организация
	|	И НЕ НастройкиЯндексКассы.ПометкаУдаления
	|	И НЕ НастройкиЯндексКассы.Недействительна";
	
	Запрос.УстановитьПараметр("ИдентификаторМагазина", ЭтотОбъект.ИдентификаторМагазина);
	Запрос.УстановитьПараметр("Организация",           ЭтотОбъект.Организация);
	Запрос.УстановитьПараметр("Ссылка",                ЭтотОбъект.Ссылка);
	Результат = Запрос.Выполнить();
	ТекущийДоговорУникален = Результат.Пустой();
	
	Если Не ТекущийДоговорУникален Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Если Выборка.Реквизит = "Организация" Тогда 
			ШаблонСообщения = НСтр("ru = 'В информационной базе уже существует настройка для организации %1'");
		ИначеЕсли Выборка.Реквизит = "ИдентификаторМагазина" Тогда
			ШаблонСообщения = НСтр("ru = 'В информационной базе уже существует настройка с таким идентификатором магазина %2 (%3)'");
		КонецЕсли;	
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
			ЭтотОбъект.Организация, ЭтотОбъект.ИдентификаторМагазина, Выборка.НайденныйДоговорСЯндексКассой);
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	Возврат ТекущийДоговорУникален;
	
КонецФункции

Процедура ОбработатьДополнительныеНастройки() 
	
	ДействующиеНастройки = ИнтеграцияСЯндексКассойСлужебный.ДополнительныеНастройкиЯндексКассы();
	// удаляем лишние
	НаУдаление = Новый Массив;
	Для каждого СтрокаНастроек Из ДополнительныеНастройки Цикл
		НайденнаяСтрока = ДействующиеНастройки.Найти(СтрокаНастроек.Настройка, "Настройка");
		Если НайденнаяСтрока = Неопределено Тогда
			НаУдаление.Добавить(СтрокаНастроек);
		КонецЕсли;
	КонецЦикла;
	Для каждого СтрокаНастроек Из НаУдаление Цикл
		ДополнительныеНастройки.Удалить(СтрокаНастроек);
	КонецЦикла;
	// добавляем недостающие
	Для каждого СтрокаНастроек Из ДействующиеНастройки Цикл
		НайденнаяСтрока = ДополнительныеНастройки.Найти(СтрокаНастроек.Настройка, "Настройка");
		Если НайденнаяСтрока = Неопределено Тогда
			НоваяСтрока = ДополнительныеНастройки.Добавить();
			НоваяСтрока.Настройка = СтрокаНастроек.Настройка;
			НоваяСтрока.Значение = СтрокаНастроек.ТипЗначения.ПривестиЗначение();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли