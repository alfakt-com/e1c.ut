#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныеЭлементы() Экспорт
	
	ПереченьОпераций = ПолучитьМакет("ПереченьОпераций").ПолучитьТекст();
	ТаблицаОпераций = ОбщегоНазначения.ПрочитатьXMLВТаблицу(ПереченьОпераций).Данные;
	
	Для каждого СтрокаТаблицы Из ТаблицаОпераций Цикл
		
		ЭлементСправочника = НайтиПоКоду(СтрокаТаблицы.Код);
		Если Не ЗначениеЗаполнено(ЭлементСправочника) Тогда
			
			Если Булево(СтрокаТаблицы.ЭтоГруппа) Тогда
				НовыйЭлемент = СоздатьГруппу();
			Иначе
				НовыйЭлемент = СоздатьЭлемент();
				НовыйЭлемент.Родитель = НайтиПоКоду(СтрокаТаблицы.Родитель);
			КонецЕсли;
			
			НовыйЭлемент.Код = СтрокаТаблицы.Код;
			НовыйЭлемент.НаименованиеПолное = СтрокаТаблицы.Наименование;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НовыйЭлемент);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Справочник.КодыВалютныхОпераций";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	Справочник.КодыВалютныхОпераций
	|";
	
	Если Запрос.Выполнить().Пустой() Тогда
	
		НачатьТранзакцию();
		
		Попытка
			
			ЗаполнитьПредопределенныеЭлементы();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать %ИмяОбъекта%: по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяОбъекта%", ПолноеИмяОбъекта);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта,,
				ТекстСообщения);
		КонецПопытки;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли