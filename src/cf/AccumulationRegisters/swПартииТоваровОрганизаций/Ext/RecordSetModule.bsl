
Процедура ПередЗаписью(Отказ, Замещение)
	Если Не Отказ Тогда
		Регистратор = Отбор.Регистратор.Значение;
		Если Количество() = 0 Тогда
			Запрос = Новый Запрос;
			Текст = "ВЫБРАТЬ
			        |	Данные.НомерСтроки
			        |ИЗ
			        |	РегистрНакопления.swПартииТоваровОрганизаций КАК Данные
			        |ГДЕ
			        |	Данные.Регистратор = &Регистратор";
			Запрос.Текст = Текст;
			Запрос.УстановитьПараметр("Регистратор", Регистратор);
			РезультатЗапроса = Запрос.Выполнить();
			Если РезультатЗапроса.Пустой() Тогда
				ДополнительныеСвойства.Вставить("РанееНеБылоДвижений", Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	Если Не Отказ Тогда
		Регистратор = Отбор.Регистратор.Значение;
		Дата = Регистратор.Дата;
		Если Не ЗначениеЗаполнено(Дата) И Количество() > 0 Тогда
			Дата = ЭтотОбъект[0].Период;
		КонецЕсли;
		Организация = Неопределено;
		Если Не ТипЗнч(Регистратор) = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
			Организация = Регистратор.Организация;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Организация) И Количество() > 0 Тогда
			Организация = ЭтотОбъект[0].Организация;
		КонецЕсли;
		ОчиститьРегистрацию = (Количество() = 0);
		НужноДвигатьПоследовательность = Истина;
		Если Не (ОчиститьРегистрацию И ДополнительныеСвойства.Свойство("РанееНеБылоДвижений")) Тогда
			swДвиженияТоваровОрганизаций.ЗарегистрироватьДвиженияВПоследовательности(Регистратор, Дата, Организация, ОчиститьРегистрацию);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
